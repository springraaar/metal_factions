#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, stand, logo1, logo2, ldoor, rdoor, turret, left, right, slholder, sleeve, barrel1, flare1, barrel2, flare2, tlogo, s22,s32, aimpt;

static-var gun_1, isOpen;

// Signal definitions
#define SIG_AIM				2


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}


open()
{
	set ARMORED to 0;
	turn rdoor to z-axis <-90.000000> speed <180.000000>;
	turn ldoor to z-axis <90.000000> speed <180.000000>;
	wait-for-turn ldoor around z-axis;
	move rdoor to y-axis  [-25.000000] speed [30.000000];
	move ldoor to y-axis  [-25.000000] speed [30.000000];
	wait-for-move ldoor along y-axis;
	move stand to y-axis [44.000000] speed [80.000000];
	wait-for-move stand along y-axis;
	isOpen = TRUE;
	return (0);
}

close()
{
	isOpen = FALSE;
	turn turret to y-axis <45.000000> speed <300.000000>;
	turn sleeve to x-axis <0> speed <300.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;

	move stand to y-axis [0.000000] speed [80.000000];
	wait-for-move stand along y-axis;

	move rdoor to y-axis  [0.000000] speed [40.000000];
	move ldoor to y-axis  [0.000000] speed [40.000000];
	wait-for-move ldoor along y-axis;

	turn rdoor to z-axis <0.000000> speed <180.000000>;
	turn ldoor to z-axis <0.000000> speed <180.000000>;
	wait-for-turn ldoor around z-axis;
	
	set ARMORED to 1;
	return (0);
}


Create()
{
	move aimpt to y-axis [45.000000] now;
	turn turret to y-axis <45.000000> now;
	start-script SmokeUnit();
	start-script close();
	isOpen = FALSE;
	gun_1 = [0];
	return (0);
}

RestoreAfterDelay()
{
	sleep 3000;
	turn turret to y-axis <0.000000> speed <200.000000>;
	turn sleeve to x-axis <0.000000> speed <200.000000>;
	call-script close();
	return (0);
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if(!isOpen)
	{
		call-script open();
	}
	
	turn turret to y-axis heading speed <200.071429>;
	turn sleeve to x-axis <0.000000> - pitch speed <150.038462>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{

	// turn barrel to next position
	if( gun_1 == [0] )
	{
		emit-sfx 1024 + 0 from flare1;	
		sleep 100;
		gun_1 = [1];
		return (0);
	}
	if( gun_1 == [1] )
	{
		emit-sfx 1024 + 0 from flare2;		
		sleep 100;
		gun_1 = [0];
		return (0);
	}
}

AimFromPrimary(piecenum)
{
	piecenum = aimpt;
	return (0);
}

QueryPrimary(piecenum)
{
	if (gun_1 == [0]) 
	{
		piecenum = flare1;
		return (0);	
	}
	if (gun_1 == [1]) 
	{
		piecenum = flare2;
		return (0);	
	}	
}

SweetSpot(piecenum)
{
	piecenum = turret;
	return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		corpsetype = 1;
		explode sleeve type SHATTER | BITMAP4;
		explode barrel2 type SHATTER | BITMAP4;
		explode barrel1 type SHATTER | BITMAP4;
		explode s22 type SHATTER | BITMAP4;
		explode s32 type SHATTER | BITMAP4;		
		return (0);
	}
	if( severity <= 60 )
	{
		corpsetype = 2;
		explode base type SHATTER | BITMAP4;
		explode stand type SHATTER | BITMAP4;
		explode turret type SHATTER | BITMAP4;
		explode sleeve type SHATTER | BITMAP4;
		explode barrel2 type SHATTER | BITMAP4;
		explode barrel1 type SHATTER | BITMAP4;
		explode ldoor type SHATTER | BITMAP4;
		explode rdoor type SHATTER | BITMAP4;

		return (0);
	}
	corpsetype = 3;
	explode base type SHATTER | BITMAP4;
	explode stand type SHATTER | BITMAP4;
	explode turret type SHATTER | BITMAP4;
	explode sleeve type SHATTER | BITMAP4;
	explode barrel2 type SHATTER | BITMAP4;
	explode barrel1 type SHATTER | BITMAP4;
	explode ldoor type SHATTER | BITMAP4;
	explode rdoor type SHATTER | BITMAP4;
	return (0);
}
