#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 100

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  base, pivot, wake1, wake2, turret, luparm, larm, l1,l2,l3,lgun,lg,lg2,lflare,lflare2, ruparm, r1,r2,r3,rgun,rg,rflare,head,eye1,eye2, spinner, bottom, b2, d2, s1, s2,s3,d3, base1, base2,base3;

static-var  bAiming, bSwimming, gun_2, terrainStatus, waterSpeed, landSpeed;

#include "aimhelper1.h"

// Signal definitions
#define SIG_MOVE			2
#define SIG_AIM				4

#include "spinbot.h"


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	var Func_Var_4;
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from turret;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

stopWheels()
{
	if (get BUILD_PERCENT_LEFT) 
	{
		stop-spin spinner around y-axis;
		return (0);
	}
	spin spinner around y-axis speed <200.000000>;
	return (0);
}


walk(last)
{
	last = 0;
	while ( TRUE )
	{
		
		if ( last != (wheelSpeed / 8) )
		{
			spin spinner around y-axis speed <200.000000> + wheelSpeed; // accelerate wheelAcceleration;
			last = wheelSpeed / 8;
		}
		turn pivot to x-axis pivotAngle speed <30.000000>;
		sleep 100;
	}
	return (0);
}



closelegs()
{
	// nothing to do here
	call-script stopWheels();
	sleep 100;
	return (0);
}

openlegs()
{
	// nothing to do here
	sleep 100;

	return (0);
}

swim()
{
	sleep 500;
	call-script closelegs();
	return (0);
}

MotionControl(justMoved)
{
	justMoved = TRUE;
	while( TRUE )
	{
		// over water
		if( ( !bSwimming ) AND (terrainStatus == 2 OR terrainStatus == 1))
		{
			call-script closelegs();
			bSwimming = TRUE;
			justMoved = TRUE;
		}
		// over land
		if( bSwimming AND terrainStatus == 4 )
		{
			call-script openlegs();
			bSwimming = FALSE;
			justMoved = TRUE;
		}
		if( bMoving )
		{
			if( bSwimming )
			{
				//set MAX_SPEED to waterSpeed; // REPLACED BY GADGET
				//set UPRIGHT to TRUE;
				call-script walk();
			}
			if( !bSwimming )
			{
				//set MAX_SPEED to landSpeed; // REPLACED BY GADGET
				//set UPRIGHT to FALSE;
				call-script walk();
			}
			justMoved = TRUE;
		}
		if( !bMoving )
		{
			if( justMoved AND ( !bSwimming ) )
			{
				call-script stopWheels();
	
				justMoved = FALSE;
			}
			sleep 100;
		}
	}
	return (0);
}

Create()
{
	landSpeed = get MAX_SPEED;
	waterSpeed = landSpeed * 100/150;
	
	bMoving = FALSE;
	bAiming = FALSE;
	bSwimming = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionControl();
	start-script WheelSpeedControl();
	start-script SmokeUnit();
	return (0);
}

StartMoving()
{
	bMoving = TRUE;
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while( TRUE )
	{
		if( terrainStatus == 2 )
		{
			emit-sfx 2 from wake1;
			emit-sfx 2 from wake2;
		}
		if( terrainStatus == 1 )
		{
			emit-sfx 2 from wake1;
			emit-sfx 2 from wake2;
		}
		sleep 250;
	}
	return (0);
}

StopMoving()
{
	bMoving = FALSE;
	signal SIG_MOVE;
	return (0);
}

setSFXoccupy(Func_Var_1)
{
	terrainStatus = Func_Var_1;
	return (0);
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<100>,<100>);
	turn turret to y-axis <0.000000> speed <100.000000>;
	turn luparm to x-axis <0.000000> speed <100.000000>;
	turn ruparm to x-axis <0.000000> speed <100.000000>;
	return (0);
}


AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<240>,<240>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	turn turret to y-axis heading speed <240.000000>;
	turn ruparm to x-axis <0.000000> - pitch speed <240.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn ruparm around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}


FirePrimary()
{
	emit-sfx 1024 from rflare;
	return (1);
}


AimFromPrimary(piecenum)
{
	piecenum = turret;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = rflare;
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = turret;
	return (0);
}



AimSecondary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<240>,<240>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	turn turret to y-axis heading speed <240.000000>;
	turn luparm to x-axis <0.000000> - pitch speed <240.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn luparm around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}


FireSecondary()
{
	if (gun_2 == [0])
	{
		emit-sfx 1024+0 from lflare;
		sleep 100;
		gun_2 = [1];
		return (1);
	}
	if (gun_2 == [1])
	{
		emit-sfx 1024+0 from lflare2;
		sleep 100;
		gun_2 = [0];
		return (1);
	}
	
}


AimFromSecondary(piecenum)
{
	piecenum = turret;
	return (0);
}

QuerySecondary(piecenum)
{
	if (gun_2 == [0])
	{
		piecenum = lflare;
		return (0);
	}
	if (gun_2 == [1])
	{
		piecenum = lflare2;
		return (0);
	}
}


Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode head type SHATTER;
		explode base2 type SHATTER;
		return (1);
	}
	explode base2 type SHATTER;
	explode base1 type SHATTER;
	explode turret type SHATTER;
	explode head type SHATTER;
	explode luparm type SHATTER;
	explode ruparm type SHATTER;
	explode spinner type SHATTER;
	explode lg type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode lg2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode rg type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	
	return (2);
}
