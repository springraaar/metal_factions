#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 180

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  gp, hips, front, back,lbackleg,lb2,rbackleg,rb2,rfrontleg,rf2,lfrontleg,lf2, body,fb2,turret,sleeves,barrel1,barrel2,barrel3,flare1,flare2,flare3;

static-var  bMoving, bAiming, isUnderWater, mASpdFactor, gun_1;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2
#define SIG_MOVE			8

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<100>,<100>);
	turn turret to y-axis <0.000000> speed <100.021978>;
	turn sleeves to x-axis <0.000000> speed <100.010989>;
	return;
}


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}


walk()
{
	if( bMoving )
	{
		
		turn hips to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;
		turn hips to z-axis <-1.000000> speed <15.000000> * mASpdFactor / 100;
		turn front to y-axis <5> speed <15> * mASpdFactor / 100;
		turn back to y-axis <-5> speed <15> * mASpdFactor / 100;

		turn lfrontleg to y-axis <20.000000> speed <100.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <-0.000000> speed <300.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		
		turn rfrontleg to y-axis <40.412088> speed <300.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <-40.093407> speed <100.368132> * mASpdFactor / 100;
		turn rbackleg to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		sleep 200 * 100 / mASpdFactor;
	}

	// only vertical
	if( bMoving )
	{

		turn lbackleg to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		turn hips to x-axis <-2.000000> speed <20.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <15.000000> * mASpdFactor / 100;
	
		sleep 200 * 100 / mASpdFactor;
	}
	
	
	if( bMoving )
	{
		turn hips to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;
		turn hips to z-axis <-1.000000> speed <15.000000> * mASpdFactor / 100;
		turn front to y-axis <-5> speed <15> * mASpdFactor / 100;
		turn back to y-axis <5> speed <15> * mASpdFactor / 100;


		turn lfrontleg to y-axis <-40.000000> speed <300.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <40.000000> speed <100.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn rfrontleg to y-axis <-20.412088> speed <100.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <00.093407> speed <300.368132> * mASpdFactor / 100;
		turn rbackleg to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		
		sleep 200 * 100 / mASpdFactor;
	}
	
	// only vertical
	if( bMoving )
	{
		turn lfrontleg to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rbackleg to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		turn hips to x-axis <-2.000000> speed <20.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <15.000000> * mASpdFactor / 100;
		
		sleep 200 * 100 / mASpdFactor;
	}
	
}



SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from body;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MotionControl(moving, aiming, justmoved)
{
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			justmoved = TRUE;
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn lbackleg to x-axis <0.000000> speed <260.000000>;
				turn rbackleg to x-axis <0.000000> speed <260.000000>;
				turn rfrontleg to x-axis <0.000000> speed <260.000000>;
				turn lfrontleg to x-axis <0.000000> speed <260.000000>;
				turn lbackleg to y-axis <25.000000> speed <260.000000>;
				turn rbackleg to y-axis <-25.000000> speed <260.000000>;
				turn rfrontleg to y-axis <25.000000> speed <260.000000>;
				turn lfrontleg to y-axis <-25.000000> speed <260.000000>;
			
				turn lbackleg to z-axis <0.000000> speed <260.000000>;
				turn rbackleg to z-axis <0.000000> speed <260.000000>;
				turn rfrontleg to z-axis <0.000000> speed <260.000000>;
				turn lfrontleg to z-axis <0.000000> speed <260.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <260.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];
				turn front to y-axis <0> speed <20> * mASpdFactor / 100;
				turn back to y-axis <0> speed <20> * mASpdFactor / 100;


				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	isUnderWater = FALSE;
	gun_1 = 0;
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
}


StartMoving()
{
	bMoving = TRUE;
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while( bMoving )
	{
		if( isUnderWater == TRUE )
		{
			//emit-sfx 256 | 3 from bleg;
			//emit-sfx 256 | 3 from bleg2;
			//emit-sfx 256 | 3 from bleg3;
			//emit-sfx 256 | 3 from bleg4;			
		}
		sleep 250;
	}
}

StopMoving()
{
	bMoving = FALSE;
	signal SIG_MOVE;
}


setSFXoccupy()
{
	isUnderWater = bMoving;
}



AimFromWeapon1(piecenum)
{
	piecenum = turret;
	return;
}

QueryWeapon1(piecenum)
{
	if( gun_1 == 0 )
	{
		piecenum = flare1;
	}
	else if( gun_1 == 1 )
	{
		piecenum = flare2;
	}
	else
	{
		piecenum = flare3;
	}
	return;
}

AimWeapon1(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<200>,<200>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <200.021978>;
	turn sleeves to x-axis <0.000000> - pitch speed <200.010989>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn sleeves around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	if( gun_1 == 0 )
	{
		move barrel1 to z-axis [-1.000000] now;
		emit-sfx 1024 from flare1;
		sleep 50;
		move barrel1 to z-axis [0.000000] speed [4.000000];
		gun_1 = 1;
	}
	else if( gun_1 == 1 )
	{
		move barrel2 to z-axis [-1.000000] now;
		emit-sfx 1024 from flare2;
		sleep 50;
		move barrel2 to z-axis [0.000000] speed [4.000000];
		gun_1 = 2;
	}	
	else
	{
		move barrel3 to z-axis [-1.000000] now;
		emit-sfx 1024 from flare3;
		sleep 50;
		move barrel3 to z-axis [0.000000] speed [4.000000];
		gun_1 = 0;
	}
	return;
}




Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode body type SHATTER;
		explode turret type SHATTER;
		explode sleeves type SHATTER;
		explode front type SHATTER;
		
		return (1);
	}
	explode body type SHATTER;
	explode turret type SHATTER;
	explode sleeves type SHATTER;
	explode front type SHATTER;
	explode back type SHATTER;
	explode lfrontleg type SHATTER;
	explode rfrontleg type SHATTER;
	explode lbackleg type SHATTER;
	explode rbackleg type SHATTER;

	return (2);
}
