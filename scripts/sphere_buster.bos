#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 270

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  GP, base, legs,lfrontleg, lmidleg,rmidleg, rfrontleg,lbackleg,rbackleg, rmid2, rback2, lback2,rfront2,lmid2,lfront2,body,sphere,sphere2, turret, sleeve, barrel, flash, front, turbase, bomb;

static-var  bMoving,bAiming, nanopiece, justmoved, mASpdFactor;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2

MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}


walk()
{
	if( bMoving )
	{
		turn rmid2 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lmid2 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		
		turn base to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;
		turn base to z-axis <0.000000> speed <15.000000> * mASpdFactor / 100;

		turn lfrontleg to y-axis <20.000000> speed <150.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <5.016484> speed <750.104396> * mASpdFactor / 100;

		turn lmidleg to y-axis <-20.000000> speed <300.038462> * mASpdFactor / 100;
		turn lmidleg to z-axis <-15.016484> speed <90.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <55.060440> speed <120.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <5.016484> speed <750.104396> * mASpdFactor / 100;
		
		turn rfrontleg to y-axis <50.412088> speed <450.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <15.016484> speed <90.104396> * mASpdFactor / 100;
		
		turn rmidleg to y-axis <-20.093407> speed <150.368132> * mASpdFactor / 100;
		turn rmidleg to z-axis <-5.016484> speed <750.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <15.137363> speed <450.681319> * mASpdFactor / 100;
		turn rbackleg to z-axis <15.016484> speed <90.104396> * mASpdFactor / 100;
		
		
		sleep 134 * 100 / mASpdFactor;
	}

	// only vertical
	if( bMoving )
	{
		turn base to x-axis <-2.000000> speed <20.000000> * mASpdFactor / 100;
		turn base to z-axis <1.000000> speed <15.000000> * mASpdFactor / 100;

		turn lmidleg to z-axis <5.016484> speed <225.104396> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-5.016484> speed <255.104396> * mASpdFactor / 100;
		turn rbackleg to z-axis <-5.016484> speed <225.104396> * mASpdFactor / 100;
		
		sleep 134 * 100 / mASpdFactor;
	}
	
	
	if( bMoving )
	{
		turn base to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;
		turn base to z-axis <0.000000> speed <15.000000> * mASpdFactor / 100;

		turn lfrontleg to y-axis <-50.000000> speed <450.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <-15.016484> speed <90.104396> * mASpdFactor / 100;

		turn lmidleg to y-axis <20.000000> speed <150.038462> * mASpdFactor / 100;
		turn lmidleg to z-axis <5.016484> speed <750.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <-15.060440> speed <450.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <-15.016484> speed <90.104396> * mASpdFactor / 100;

		turn rfrontleg to y-axis <-20.412088> speed <150.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-5.016484> speed <750.104396> * mASpdFactor / 100;
		
		turn rmidleg to y-axis <20.093407> speed <450.368132> * mASpdFactor / 100;
		turn rmidleg to z-axis <15.016484> speed <90.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <-55.137363> speed <180.681319> * mASpdFactor / 100;
		turn rbackleg to z-axis <-5.016484> speed <750.104396> * mASpdFactor / 100;
		
		sleep 134 * 100 / mASpdFactor;
	}
	
	// only vertical
	if( bMoving )
	{
		turn base to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;
		turn base to z-axis <-1.000000> speed <15.000000> * mASpdFactor / 100;

		turn rmid2 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lmid2 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;

		turn lfrontleg to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn lbackleg to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rmidleg to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		
		sleep 134 * 100 / mASpdFactor;
	}
	
}



Stop()
{
	turn base to z-axis <0.000000> speed <7.000000>;
	turn base to x-axis <0.000000> speed <7.000000>;
	turn lfrontleg to y-axis <0.000000> speed <237.000000>;
	turn lfrontleg to z-axis <0.000000> speed <237.000000>;
	turn lmidleg to y-axis <0.000000> speed <237.000000>;
	turn lmidleg to z-axis <0.000000> speed <237.000000>;
	turn lbackleg to y-axis <0.000000> speed <237.000000>;
	turn lbackleg to z-axis <0.000000> speed <237.000000>;
	turn rbackleg to y-axis <0.000000> speed <237.000000>;
	turn rbackleg to z-axis <0.000000> speed <237.000000>;
	turn rmidleg to y-axis <0.000000> speed <237.000000>;
	turn rmidleg to z-axis <0.000000> speed <237.000000>;
	turn rfrontleg to y-axis <0.000000> speed <237.000000>;
	turn rfrontleg to z-axis <0.000000> speed <237.000000>;
	turn lbackleg to y-axis <20.000000> speed <300.000000>;
	turn rbackleg to y-axis <-20.000000> speed <300.000000>;
	turn rfrontleg to y-axis <30.000000> speed <300.000000>;
	turn lfrontleg to y-axis <-30.000000> speed <300.000000>;

}



MotionControl()
{
	while( TRUE )
	{
		if( bMoving )
		{
			justmoved=TRUE;
			call-script walk();
		}
		if( !bMoving )
		{
			if(justmoved)
			{
				call-script Stop();
				justmoved=FALSE;
			}
			sleep 100;
		}
	}
}



StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

Create()
{
	
	bMoving = FALSE;
	justmoved = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
	
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<100>,<100>);
	turn turret to y-axis <0.000000> speed <100.000000>;
	turn sleeve to x-axis <0.000000> speed <100.000000>;
}



AimPrimary(heading,pitch)
{
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;

	turn turret to y-axis heading speed <300.000000>;
	turn sleeve to x-axis <0.000000> - pitch speed <300.000000>;
	if (!aimReady1)
	{
		wait-for-turn sleeve around x-axis;
		wait-for-turn turret around y-axis;
	}
	
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	emit-sfx 1024 from flash;
	move bomb to z-axis [-5] now;
	sleep 8000;
	move bomb to z-axis [0] speed [15];
	
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum=turret;
	return 0;
}

QueryPrimary(piecenum)
{
	piecenum = flash;
}


SweetSpot(piecenum)
{
	piecenum = body;
}

Killed(severity, corpsetype)
{
	if( severity <= 35 )
	{
		explode front type SHATTER | BITMAP2;
		explode turbase type SHATTER | BITMAP2;
		return (1);
	}
	explode base type BITMAPONLY | BITMAP1;
	explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode front type SHATTER | BITMAP2;
	explode turbase type SHATTER | BITMAP2;
	explode sphere type SHATTER | BITMAP2;
	explode lfrontleg type SHATTER | BITMAP2;
	explode lmidleg type SHATTER | BITMAP2;
	explode lbackleg type SHATTER | BITMAP2;
	explode rfrontleg type SHATTER | BITMAP2;
	explode rmidleg type SHATTER | BITMAP2;
	explode rbackleg type SHATTER | BITMAP2;
	return (2);
}
