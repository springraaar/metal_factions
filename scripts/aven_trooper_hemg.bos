#define TAK			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 200

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  base,torso,head,pelvis, flash, rthigh, lthigh, lleg, rleg, lfoot,rfoot,ruparm,rarm,luparm, larm, lloarm, lhand, gun, pack,dustbase,dustpt1,dustpt2,dustpt3,barrier1,barrier2,barrier3, tshield1,tshield2,tshield3;

static-var  bMoving, bAiming, mATurnSpd1,mATurnSpd2,mATurnSpd3,mASpdFactor, mAMoveSpd1, mAVertFactor, legAngle1, legAngle2, legAngle3, isDeployed, isDeploying;

#include "aimhelper1.h"

lua_setMobility() { return 0; }

// Signal definitions
#define SIG_AIM				2


shake()
{
	set CEG_DAMAGE to 12;
	play-sound( "burrow2", 3 );
	spin dustbase around y-axis speed <920>;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt2;
	emit-sfx 1024+2 from lleg;
	emit-sfx 1024+2 from rleg;
	turn base to z-axis <1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt3;	
	turn base to z-axis <-1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt2;
	emit-sfx 1024+1 from dustpt3;	
	turn base to z-axis <1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt2;
	emit-sfx 1024+2 from lleg;
	emit-sfx 1024+2 from rleg;
	turn base to z-axis <-1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt3;
	turn base to z-axis <1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt2;
	emit-sfx 1024+1 from dustpt3;
	turn base to z-axis <-1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt2;
	turn base to z-axis <1> speed <10>;
	sleep 30;
	emit-sfx 1024+1 from dustpt1;
	emit-sfx 1024+1 from dustpt3;
	turn base to z-axis <-1> speed <10>;
	emit-sfx 1024+2 from lleg;
	emit-sfx 1024+2 from rleg;
	sleep 30;
	emit-sfx 1024+1 from dustpt2;
	emit-sfx 1024+1 from dustpt3;
	turn base to z-axis <1> speed <10>;
	sleep 30;
	turn base to z-axis <-1> speed <10>;
	sleep 30;
	turn base to z-axis <1> speed <10>;
	sleep 30;
	turn base to z-axis <-1> speed <10>;
	sleep 30;
	turn base to z-axis <0> speed <10>;
}

deploy()
{
	if (!isDeployed)
	{
		isDeploying = TRUE;
		call-script lua_setMobility(0);
		
		move barrier1 to y-axis [-12] now;
		
		// burrow
		show barrier1;
		hide barrier2;
		hide barrier3;
		show tshield1;
		hide tshield2;
		hide tshield3;

		
		move barrier1 to y-axis [-3] speed [7];

		call-script shake();
		call-script shake();

		turn luparm to x-axis <-25.000000> speed <25>;
		turn lloarm to x-axis <-25.000000> speed <40>;

		hide barrier1;
		show barrier2;
		hide barrier3;
		hide tshield1;
		show tshield2;
		hide tshield3;

		
		call-script shake();
		call-script shake();

		hide barrier1;
		hide barrier2;
		show barrier3;
		hide tshield1;
		hide tshield2;
		show tshield3;

		sleep 100;
		isDeployed = TRUE;
		isDeploying = FALSE;
	}
}

undeploy()
{
	if (isDeployed)
	{
		isDeployed = FALSE;
		isDeploying = TRUE;
		
		// unburrow
		hide barrier1;
		show barrier2;
		hide barrier3;
		hide tshield1;
		show tshield2;
		hide tshield3;

		move barrier1 to y-axis [-12] speed [7];

		call-script shake();
		call-script shake();

		show barrier1;
		hide barrier2;
		hide barrier3;
		show tshield1;
		hide tshield2;
		hide tshield3;

		call-script shake();
		call-script shake();

		turn luparm to x-axis <-15.000000> speed <25>;
		turn lloarm to x-axis <-15.000000> speed <40>;

		hide barrier1;
		hide barrier2;
		hide barrier3;
		hide tshield1;
		hide tshield2;
		hide tshield3;

		
		sleep 100;

		call-script lua_setMobility(100);
		isDeploying = FALSE;
	}
	isDeployed = FALSE;
}


handleActivation() 
{
	while (TRUE)
	{ 
		if (isDeployed)
		{
			set ARMORED to TRUE;
		}
		else
		{
			set ARMORED to FALSE;
		}
		if (get ACTIVATION) 
		{
			call-script deploy();
		}
		else
		{
			call-script undeploy();
		}
		sleep 100;
	}
}



MotionSpeedControl(maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < SPEEDUP_FACTOR/2 )
		{
			mASpdFactor = SPEEDUP_FACTOR/2;
		}
		
		
		mAMoveSpd1 = [17.000000] * mASpdFactor / 100;
		mATurnSpd1 = <285.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd2 = <170.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd3 = <60.000000> * (100 + mASpdFactor) / 200;

		mAVertFactor = [0.000000]-[1.000000] * mASpdFactor / SPEEDUP_FACTOR;
		legAngle1 = <40.000000>;
		legAngle2 = <30.000000>;
		legAngle3 = <15.000000>;
		if ( mASpdFactor < SPEEDUP_FACTOR )
		{
			legAngle1 = legAngle1 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle2 = legAngle2 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle3 = legAngle3 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);

		}
	
		sleep 60;
	}
}

walk()
{
	var slowTurnSpd, slowTurnSpd2;

	slowTurnSpd = mATurnSpd2 / 2;
	slowTurnSpd2 = mATurnSpd2 / 8;
	
	
	// back contact point : right foot behind, left foot forward
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000>-legAngle1 speed mATurnSpd1;
		turn lleg to x-axis legAngle2 speed mATurnSpd2;
		turn lfoot to x-axis <0> speed mATurnSpd3;

		turn rthigh to x-axis legAngle2 speed mATurnSpd2;
		turn rleg to x-axis legAngle3 speed mATurnSpd2;
		turn rfoot to x-axis <0.000000>-legAngle2 speed mATurnSpd1;

		move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		turn pelvis to x-axis <6.000000> speed slowTurnSpd2;
		if(!bAiming)
		{
			// turn torso left, right arm forward
			turn torso to y-axis <10.000000> speed slowTurnSpd2;
			turn luparm to x-axis <10.000000> speed slowTurnSpd;
			turn lloarm to x-axis <-10.000000> speed slowTurnSpd2;

			turn ruparm to x-axis <0.000000> speed slowTurnSpd;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 2 : left stomp ground
	if( bMoving)
	{
		turn lfoot to x-axis legAngle3 speed mATurnSpd1*3;
		turn pelvis to x-axis <-3.000000> speed slowTurnSpd2;
		wait-for-turn lfoot around x-axis;
	}

	// passing pose 2 : right foot raising, left foot on the ground
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000> speed mATurnSpd2;
		turn lleg to x-axis <0.000000> speed mATurnSpd2;
		turn lfoot to x-axis <0.000000> speed mATurnSpd3;

		turn rthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn rleg to x-axis <30.000000> speed mATurnSpd2;
		turn rfoot to x-axis <10.000000> speed mATurnSpd1;

		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn luparm to x-axis <-15.000000> speed slowTurnSpd;
			turn lloarm to x-axis <-15.000000> speed slowTurnSpd2;
			turn ruparm to x-axis <10.000000> speed slowTurnSpd;
		}

		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;		
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;

	}
	
	// forward contact point : right foot forward, left foot behind
	if( bMoving )
	{
		turn lthigh to x-axis legAngle2 speed mATurnSpd2;
		turn lleg to x-axis legAngle3 speed mATurnSpd2;
		turn lfoot to x-axis <0>-legAngle2 speed mATurnSpd1;

		turn rthigh to x-axis <0>-legAngle1 speed mATurnSpd1;
		turn rleg to x-axis legAngle2 speed mATurnSpd2;
		turn rfoot to x-axis <0> speed mATurnSpd3;
		
		move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		turn pelvis to x-axis <6.000000> speed slowTurnSpd2;
		
		if(!bAiming)
		{
			// turn torso right, left arm forward
			turn torso to y-axis <-10.000000> speed slowTurnSpd2;
			turn luparm to x-axis <-40.000000> speed slowTurnSpd;
			turn lloarm to x-axis <-20.000000> speed slowTurnSpd2;
			turn ruparm to x-axis <20.000000> speed slowTurnSpd;
		}

		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 1 : right stomp ground
	if( bMoving)
	{
		turn rfoot to x-axis legAngle3 speed mATurnSpd1*3;
		turn pelvis to x-axis <-3.000000> speed slowTurnSpd2;
		wait-for-turn rfoot around x-axis;
	}

	// passing pose 1 : right foot on ground, left foot raising
	if( bMoving )
	{
		turn lthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn lleg to x-axis <30.000000> speed mATurnSpd2;
		turn lfoot to x-axis <10.000000> speed mATurnSpd1;
		
		turn rthigh to x-axis <0.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <0.000000> speed mATurnSpd3;

		
		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn luparm to x-axis <-15.000000> speed slowTurnSpd;
			turn lloarm to x-axis <-15.000000> speed slowTurnSpd2;
			turn ruparm to x-axis <10.000000> speed slowTurnSpd;
		}
		
		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}
	
	return;
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			call-script walk();
			justmoved = TRUE;
		}
		if( !moving )
		{
			if( justmoved )
			{
				move pelvis to y-axis [0.000000] speed [25.000000];
				turn pelvis to x-axis <0.000000> speed <20.000000>;
				turn rthigh to x-axis <0.000000> speed <200.000000>;
				turn rleg to x-axis <0.000000> speed <200.000000>;
				turn lthigh to x-axis <0.000000> speed <200.000000>;
				turn lleg to x-axis <0.000000> speed <200.000000>;
				turn rfoot to x-axis <0.000000> speed <200.000000>;
				turn lfoot to x-axis <0.000000> speed <200.000000>;
				if( !aiming )
				{
					turn torso to y-axis <0.000000> speed <90.016484>;
					turn luparm to x-axis <-10.000000> speed <150.000000>;
					turn ruparm to x-axis <0.000000> speed <100.000000>;
					turn lloarm to x-axis <-20.000000> speed <150.000000>;					
				}
				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	hide barrier1;
	hide barrier2;
	hide barrier3;
	hide tshield1;
	hide tshield2;
	hide tshield3;
	move barrier1 to y-axis [-11] now;
	
	isDeployed = FALSE;
	set ARMORED to FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
	start-script handleActivation();
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

SweetSpot(piecenum)
{
	piecenum = torso;
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<200>,<200>);
	turn torso to y-axis <0.000000> speed <200.016484>;
	turn ruparm to x-axis <0.000000> speed <200.016484>;
	wait-for-turn torso around y-axis;
	wait-for-turn ruparm around x-axis;
	bAiming = FALSE;
}

AimPrimary(heading, pitch)
{
	if (isDeploying) 
	{
		return (0);
	}
	if (bMoving) 
	{
		call-script updateAimHelper1(heading,pitch,<70>,<70>,HEADING_TOLERANCE,PITCH_TOLERANCE);

		signal SIG_AIM;
		set-signal-mask SIG_AIM;
		
		bAiming = TRUE;
		turn torso to y-axis heading speed <70.016484>;
		turn ruparm to x-axis <0.000000> - pitch speed <70.016484>;
		if (!aimReady1)
		{
			wait-for-turn torso around y-axis;
			wait-for-turn ruparm around x-axis;
		}
	} else {
		call-script updateAimHelper1(heading,pitch,<200>,<200>,HEADING_TOLERANCE,PITCH_TOLERANCE);

		signal SIG_AIM;
		set-signal-mask SIG_AIM;

		bAiming = TRUE;
		turn torso to y-axis heading speed <200.016484>;
		turn ruparm to x-axis <0.000000> - pitch speed <200.016484>;
		if (!aimReady1)
		{
			wait-for-turn torso around y-axis;
			wait-for-turn ruparm around x-axis;
		}
	}
	start-script RestoreAfterDelay();
	return (1);
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
}

QueryPrimary(piecenum)
{
	piecenum = flash;
}

FirePrimary()
{
	move torso to z-axis [-0.500000] now;
	move gun to z-axis [-1.0] now;
	emit-sfx 1024 + 0 from flash;
	sleep 29;
	move torso to z-axis [0] speed [10];
	sleep 29;
	move gun to z-axis [0] speed [10];
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode head type SHATTER;
		explode ruparm type SHATTER;
		explode larm type SHATTER;
		explode luparm type SHATTER;
		explode rarm type SHATTER;
		if (isDeployed) 
		{
			return (1);
		}
		return (2);
	}
	explode head type SHATTER;
	explode ruparm type SHATTER;
	explode larm type SHATTER;
	explode luparm type SHATTER;
	explode rarm type SHATTER;
	explode lleg type SHATTER;
	explode rleg type SHATTER;
	explode torso type SHATTER;
	explode pack type SHATTER;
	return (3);
}
