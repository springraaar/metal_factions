#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 	100

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  gp, base, turret, lsup,lholder, lsleeve, lbarrel, lflash, rsup, rholder, rsleeve, rbarrel, rflash, base2,l1,l2,l3,l4,t1,t2,t1cover,t2cover;

static-var  gun_1;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2

#include "tankthreads.h"

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

RockUnit(anglex, anglez)
{
	turn base to x-axis anglex speed <50.005495>;
	turn base to z-axis <0>-anglez speed <50.005495>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.000000> speed <20.000000>;
	turn base to x-axis <0.000000> speed <20.000000>;
}


Create()
{
	gun_1=[0];
	call-script stopAimHelper1(<1000>,<1000>);
	start-script SmokeUnit();
	start-script AnimateTracks();
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<90>,<90>);
	turn turret to y-axis <0.000000> speed <90.016484>;
	turn lsleeve to x-axis <0.000000> speed <90.005495>;
	turn rsleeve to x-axis <0.000000> speed <90.005495>;
}

AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<200>,<200>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <200.038462>;
	turn lsleeve to x-axis <0.000000> - pitch speed <200.000000>;
	turn rsleeve to x-axis <0.000000> - pitch speed <200.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn lsleeve around x-axis;
		wait-for-turn rsleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	if(gun_1 == [0])
	{
		move lbarrel to z-axis [-1.000000] now;
		emit-sfx 1024 from lflash;
		sleep 50;
		move lbarrel to z-axis [0.000000] speed [1.000000];
		gun_1 = [1];
		return (0);
	}
	if(gun_1 == [1])
	{
		move rbarrel to z-axis [-1.000000] now;
		emit-sfx 1024 from rflash;
		sleep 50;
		move rbarrel to z-axis [0.000000] speed [1.000000];
		gun_1 = [0];
		return (0);
	}	
	
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if( gun_1 == [0])
	{
		piecenum = lflash;
	}
	if( gun_1 == [1])
	{
		piecenum = rflash;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode lbarrel type BITMAPONLY | BITMAP1;
		explode lsleeve type BITMAPONLY | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode lflash type BITMAPONLY | BITMAP3;
		explode turret type BITMAPONLY | BITMAP4;
		return (1);
	}
	explode lbarrel type FALL | BITMAP1;
	explode lsleeve type FALL | BITMAP1;
	explode base type BITMAPONLY | BITMAP2;
	explode lflash type FALL | BITMAP3;
	explode turret type SHATTER | BITMAP4;
	explode t1cover type SHATTER;
	explode t2cover type SHATTER;
	return (2);
}
