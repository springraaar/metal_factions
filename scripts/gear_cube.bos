#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 150

#define HEADING_TOLERANCE	<30>
#define PITCH_TOLERANCE		<20>

piece  ground, hips, leg1,bleg,leg2,bleg2,leg3,bleg3,leg4,bleg4, body, turret, sleeve, barrel, flare, cover, b2, legsup1, legsup2, legsup3, legsup4;

static-var  bMoving, isOpen, mASpdFactor;

#include "aimhelper1.h"

// Signal definitions
#define SIG_MOVE			2
#define SIG_AIM				4


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}

walk()
{
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-3.000000> speed <20.000000> * mASpdFactor / 100;
			
		/* Y */
		turn leg4 to y-axis <-80.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-35.000000> speed <120.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <5.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <25.000000> speed <260.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		
		
		turn leg4 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg4 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <3.000000> speed <20.000000> * mASpdFactor / 100;
		
		/* Y */
		turn leg4 to y-axis <-35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-90.000000> speed <180.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <15.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <-5.000000> speed <200.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * mASpdFactor / 100;
		
		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		
		turn leg1 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg1 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-3.000000> speed <20.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-5.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-25.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <80.000000> speed <260.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <35.000000> speed <120.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg2 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg3 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <-3.000000> speed <20.000000> * mASpdFactor / 100;


		/* move */
		move hips to x-axis [0.2500000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		
		turn leg3 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg3 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		turn hips to x-axis <3.000000> speed <20.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-15.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <5.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg1 to y-axis <90.000000> speed <180.000000> * mASpdFactor / 100;			

		/* Z */
		turn bleg3 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg2 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [2.000000] * mASpdFactor / 100;

		turn leg2 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg2 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
	}
}

MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		
		if( moving )
		{
			justmoved = TRUE;
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn leg1 to x-axis <0.000000> speed <360.000000>;
				turn leg2 to x-axis <0.000000> speed <360.000000>;
				turn leg3 to x-axis <0.000000> speed <360.000000>;
				turn leg4 to x-axis <0.000000> speed <360.000000>;
				turn leg1 to y-axis <45.000000> speed <360.000000>;
				turn leg2 to y-axis <-45.000000> speed <360.000000>;
				turn leg3 to y-axis <45.000000> speed <360.000000>;
				turn leg4 to y-axis <-45.000000> speed <360.000000>;
			
				turn leg1 to z-axis <0.000000> speed <360.000000>;
				turn leg2 to z-axis <0.000000> speed <360.000000>;
				turn leg3 to z-axis <0.000000> speed <360.000000>;
				turn leg4 to z-axis <0.000000> speed <360.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <360.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];

				turn bleg2 to z-axis <0.000000> speed <160.000000>;
				turn bleg4 to z-axis <0.000000> speed <160.000000>;
				turn bleg3 to z-axis <0.000000> speed <160.000000>;
				turn bleg to z-axis <0.000000> speed <160.000000>;

				justmoved = FALSE;
			}
			sleep 100;
		}
	}
	return (0);
}



SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from turret;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

StartMoving()
{
	bMoving = TRUE;
	return (0);
}

StopMoving()
{
	bMoving = FALSE;
	return (0);
}

Open()
{
	set ARMORED to FALSE;
	move turret to y-axis [7.500000] speed [20.000000];
	wait-for-move turret along y-axis;

	//move sleeve to z-axis [2.000000] speed [15.000000];
	//move barrel to z-axis [3.000000] speed [15.000000];
	//wait-for-move sleeve along z-axis;

	turn turret to y-axis <0.000000> speed <200.000000>;
	turn sleeve to x-axis <0.000000> speed <200.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;

	isOpen = TRUE;
	return (0);
}

Close()
{
	isOpen = FALSE;
	turn turret to y-axis <0.000000> speed <200.000000>;
	turn sleeve to x-axis <0.000000> speed <200.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeve around x-axis;
	//move sleeve to z-axis [0.000000] speed [5.000000];
	//move barrel to z-axis [0.000000] speed [5.000000];
	//wait-for-move sleeve along z-axis;
	move turret to y-axis [0.000000] speed [10.000000];
	wait-for-move turret along y-axis;
	
	set ARMORED to TRUE;
	return (0); 
}

RestoreAfterDelay()
{
	if((get STANDINGFIREORDERS) > 0)
	{
		sleep DEFAULT_WEAPON_RESTORE_DELAY;
	} 
	else 
	{
		sleep 600;
	}
	call-script stopAimHelper1(<200>,<200>);
	call-script Close();
}
Create()
{
	isOpen = FALSE;
	bMoving = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = body;
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = flare;
	return (0);
}


AimPrimary(heading, pitch)
{
	if( ! isOpen)
	{
		call-script Open();
	}
	
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;

	turn turret to y-axis heading speed <300.000000>;
	//turn sleeve to x-axis <0.000000> - pitch speed <280.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		//wait-for-turn sleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	emit-sfx 1024 from flare;
	return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode cover type SHATTER | BITMAP2;
		explode legsup1 type SHATTER | BITMAP2;
		explode legsup2 type SHATTER | BITMAP2;
		explode legsup3 type SHATTER | BITMAP2;
		explode legsup4 type SHATTER | BITMAP2;
		return (1);
	}
	explode body type SHATTER | BITMAP2;
	explode turret type SHATTER | BITMAP2;
	explode b2 type SHATTER | BITMAP2;	
	explode cover type SHATTER | BITMAP2;
	explode legsup1 type SHATTER | BITMAP2;
	explode legsup2 type SHATTER | BITMAP2;
	explode legsup3 type SHATTER | BITMAP2;
	explode legsup4 type SHATTER | BITMAP2;
	explode leg1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg3 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg4 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	return (2);
}
