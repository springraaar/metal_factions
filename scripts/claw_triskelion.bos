#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 110

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  gp, hips, base2, front, back,lbackleg,lb2,rbackleg,rb2,rfrontleg,rf2,lfrontleg,lf2, turret1, holder1, sleeve1, barrel1, flare1, turret2, holder2, sleeve2, barrel2, flare2,turret3, holder3, sleeve3, barrel3, flare3;

static-var  bMoving, bAiming, mASpdFactor;

#include "aimhelper1.h"
#include "aimhelper2.h"
#include "aimhelper3.h"

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM2			4
#define SIG_AIM3			8


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}


walk()
{
	if( bMoving )
	{
		
		turn hips to x-axis <2.000000> speed <10.000000> * mASpdFactor / 100;
		//turn hips to z-axis <-1.000000> speed <15.000000> * mASpdFactor / 100;
		move hips to z-axis [-1] speed [5] * mASpdFactor / 100;

		turn lfrontleg to y-axis <20.000000> speed <100.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <3.016484> speed <550.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <-0.000000> speed <300.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		
		turn rfrontleg to y-axis <30.412088> speed <300.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <-30.093407> speed <100.368132> * mASpdFactor / 100;
		turn rbackleg to z-axis <-3.016484> speed <550.104396> * mASpdFactor / 100;
		
		sleep 200 * 100 / mASpdFactor;
	}

	// only vertical
	if( bMoving )
	{
		turn hips to x-axis <-2.000000> speed <10.000000> * mASpdFactor / 100;
		move hips to z-axis [1] speed [5] * mASpdFactor / 100;
		turn lbackleg to z-axis <3.016484> speed <150.104396> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-3.016484> speed <150.104396> * mASpdFactor / 100;
		
		sleep 150 * 100 / mASpdFactor;
	}
	
	
	if( bMoving )
	{
		turn hips to x-axis <2.000000> speed <10.000000> * mASpdFactor / 100;
		move hips to z-axis [-1] speed [5] * mASpdFactor / 100;
 		//turn hips to z-axis <1.000000> speed <15.000000> * mASpdFactor / 100;

		turn lfrontleg to y-axis <-30.000000> speed <300.967033> * mASpdFactor / 100;
		turn lfrontleg to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lbackleg to y-axis <30.000000> speed <100.038462> * mASpdFactor / 100;
		turn lbackleg to z-axis <3.016484> speed <550.104396> * mASpdFactor / 100;

		turn rfrontleg to y-axis <-20.412088> speed <100.060440> * mASpdFactor / 100;
		turn rfrontleg to z-axis <-3.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rbackleg to y-axis <00.093407> speed <300.368132> * mASpdFactor / 100;
		turn rbackleg to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		
		sleep 200 * 100 / mASpdFactor;
	}
	
	// only vertical
	if( bMoving )
	{
		turn hips to x-axis <-2.000000> speed <10.000000> * mASpdFactor / 100;
		move hips to z-axis [1] speed [5] * mASpdFactor / 100;
		turn lfrontleg to z-axis <3.016484> speed <150.104396> * mASpdFactor / 100;
		turn rbackleg to z-axis <-3.016484> speed <150.104396> * mASpdFactor / 100;
		
		
		sleep 150 * 100 / mASpdFactor;
	}
	
}



SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from turret1;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MotionControl(moving, aiming, justmoved)
{
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			justmoved = TRUE;
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn lbackleg to x-axis <0.000000> speed <260.000000>;
				turn rbackleg to x-axis <0.000000> speed <260.000000>;
				turn rfrontleg to x-axis <0.000000> speed <260.000000>;
				turn lfrontleg to x-axis <0.000000> speed <260.000000>;
				turn lbackleg to y-axis <45.000000> speed <260.000000>;
				turn rbackleg to y-axis <-45.000000> speed <260.000000>;
				turn rfrontleg to y-axis <45.000000> speed <260.000000>;
				turn lfrontleg to y-axis <-45.000000> speed <260.000000>;
			
				turn lbackleg to z-axis <0.000000> speed <260.000000>;
				turn rbackleg to z-axis <0.000000> speed <260.000000>;
				turn rfrontleg to z-axis <0.000000> speed <260.000000>;
				turn lfrontleg to z-axis <0.000000> speed <260.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <260.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];


				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	call-script stopAimHelper2(<1000>,<1000>);
	call-script stopAimHelper3(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
}


StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}


RestoreAfterDelay1()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<90>,<90>);
	turn turret1 to y-axis <0.000000> speed <90.016484>;
	turn sleeve1 to x-axis <0.000000> speed <90.005495>;
}
RestoreAfterDelay2()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper2(<120>,<120>);
	turn turret2 to y-axis <0.000000> speed <120.016484>;
	turn sleeve2 to x-axis <0.000000> speed <120.005495>;
}
RestoreAfterDelay3()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper3(<140>,<140>);
	turn turret3 to y-axis <0.000000> speed <140.016484>;
	turn sleeve3 to x-axis <0.000000> speed <140.005495>;
}



AimFromPrimary(piecenum)
{
	piecenum = turret1;
}

QueryPrimary(piecenum)
{
	piecenum = flare1;
	return (0);
}

FirePrimary()
{
	emit-sfx 1024 + 0 from flare1;
	return (0);
}

AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	
	turn turret1 to y-axis heading speed <300.027473>;
	turn sleeve1 to x-axis <0.000000> - pitch speed <300.038462>;
	if (!aimReady1)
	{
		wait-for-turn turret1 around y-axis;
		wait-for-turn sleeve1 around x-axis;
	}
	start-script RestoreAfterDelay1();
	return (1);
}



AimFromSecondary(piecenum)
{
	piecenum = turret2;
}

QuerySecondary(piecenum)
{
	piecenum = flare2;
	return (0);
}

FireSecondary()
{
	emit-sfx 1024 + 0 from flare2;
	return (0);
}

AimSecondary(heading, pitch)
{
	call-script updateAimHelper2(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2;
	
	turn turret2 to y-axis heading speed <300.027473>;
	turn sleeve2 to x-axis <0.000000> - pitch speed <300.038462>;
	if (!aimReady2)
	{
		wait-for-turn turret2 around y-axis;
		wait-for-turn sleeve2 around x-axis;
	}
	start-script RestoreAfterDelay2();
	return (1);
}

AimFromTertiary(piecenum)
{
	piecenum = turret3;
}

QueryTertiary(piecenum)
{
	piecenum = flare3;
	return (0);
}

FireTertiary()
{
	emit-sfx 1024 + 0 from flare3;
	return (0);
}

AimTertiary(heading, pitch)
{
	call-script updateAimHelper3(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3;
	
	turn turret3 to y-axis heading speed <300.027473>;
	turn sleeve3 to x-axis <0.000000> - pitch speed <300.038462>;
	if (!aimReady3)
	{
		wait-for-turn turret3 around y-axis;
		wait-for-turn sleeve3 around x-axis;
	}
	start-script RestoreAfterDelay3();
	return (1);
}


Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode base2 type SHATTER;
		explode front type SHATTER;
		
		return (1);
	}
	explode base2 type SHATTER;
	explode front type SHATTER;
	explode rfrontleg type  SHATTER;
	explode rbackleg type SHATTER;
	explode lfrontleg type SHATTER;
	explode lbackleg type SHATTER;
	return (2);
}
