#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turbase, turret, sleeve, llauncher, lflare,rlauncher,rflare,r2,l2, b1,b2,bottom, sides, aimpt;

static-var  gun_1,canAim;

// Signal definitions
#define SIG_AIM				2

lua_stopFiring() { return 0; }

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Open()
{
	SET ARMORED to FALSE;

	move turbase to y-axis [40] speed [160];
	wait-for-move turbase along y-axis;

	canAim = TRUE;
}


Close()
{
	canAim = FALSE;

	//turn sleeve to x-axis <0.000000> speed <300.000000>;
	//turn turret to y-axis <0.000000> speed <300.000000>;
	move turbase to y-axis [0] speed [160];
	//wait-for-turn sleeve around x-axis;
	//wait-for-turn turret around y-axis;
	wait-for-move turbase along y-axis;

	SET ARMORED to TRUE;
}


RestoreAfterDelay()
{
	if((get STANDINGFIREORDERS) > 0)
	{
		sleep DEFAULT_WEAPON_RESTORE_DELAY;
	} 
	else 
	{
		sleep 600;
	}
	call-script Close();
}

// if fire state changes to hold fire, force closure
checkClose(fireState,previousFireState)
{
	while (TRUE)
	{
		fireState = (get STANDINGFIREORDERS);
		
		if (fireState == 0 && previousFireState > 0) 
		{
			signal SIG_AIM;
			call-script lua_stopFiring();
			call-script RestoreAfterDelay();
		}
	
		previousFireState = fireState;
		sleep 100;
	}
}

Create()
{
	hide lflare;
	hide rflare;
	move aimpt to y-axis [40.000000] now;
	gun_1=[0];
	canAim = FALSE;
	start-script SmokeUnit();
	start-script Close();
	start-script checkClose();
}


SweetSpot(piecenum)
{
	piecenum = turret;
}

AimFromPrimary(piecenum)
{
	piecenum = aimpt;
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if (canAim == FALSE)
	{
		call-script Open();
	}
	//turn sleeve to x-axis <0.000000> - pitch speed <200.000000>;
	turn turret to y-axis heading speed <300.000000>;
	//wait-for-turn sleeve around x-axis;
	wait-for-turn turret around y-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if (gun_1 == [0])
	{
		emit-sfx 1024+0 from lflare;
		sleep 50;
		gun_1 = [1];
		return(1);
	}
	if (gun_1 == [1])
	{
		emit-sfx 1024+0 from rflare;
		sleep 50;
		gun_1 = [0];
		return(1);
	}
}

QueryPrimary(piecenum)
{
	if (gun_1 == [0])
	{
		piecenum = lflare;
	}
	if (gun_1 == [1])
	{
		piecenum = rflare;
	}
}

Killed(severity, corpsetype)
{
	hide lflare;
	hide rflare;
	
	if( severity <= 50 )
	{
		return (1);
	}

	explode llauncher type SHATTER | BITMAP4;
	explode rlauncher type SHATTER | BITMAP4;
	explode sides type SHATTER | BITMAP4;
	return (2);
}
