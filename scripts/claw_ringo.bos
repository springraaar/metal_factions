#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 200

#define HEADING_TOLERANCE	<35>
#define PITCH_TOLERANCE		<25>

piece  turret, flare, front,back,ruparm, luparm, pelvis, lthigh,
       rthigh, rleg, rfoot, lleg, lfoot, lloarm, lhand, rloarm,turbase,faxis,baxis,
       rhand, ground, ringholder,ring1,ring2,ring3,ring4, ringfx;

static-var  bMoving, bAiming, mAVertFactor, mATurnSpd1,mATurnSpd2,mATurnSpd3,mASpdFactor, mAMoveSpd1, mADelay1, legAngle1, legAngle2, legAngle3, legAngle4, shouldRun, reloadTime, isReady, chargeState;

// Signal definitions
#define SIG_MOVE			2
#define SIG_AIM				4
#define SIG_RESET			8

lua_resetReload() { return 0; }

MotionSpeedControl(maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		shouldRun = FALSE;
		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < 3 * SPEEDUP_FACTOR / 4 )
		{
			mASpdFactor = 3 * SPEEDUP_FACTOR / 4;
		}
		
		if ( mASpdFactor > 3 * SPEEDUP_FACTOR / 4 ) {
			shouldRun = TRUE;
		}
		
		
		mAMoveSpd1 = [20.000000] * mASpdFactor / 100;
		mATurnSpd1 = <415.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd2 = <300.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd3 = <150.000000> * (100 + mASpdFactor) / 200;

		mAVertFactor = [0.000000]-[3.000000] * mASpdFactor / SPEEDUP_FACTOR;
		legAngle1 = <45.000000>;
		legAngle2 = <35.000000>;
		legAngle3 = <15.000000>;
		legAngle4 = <0.000000>;
		if ( mASpdFactor < SPEEDUP_FACTOR )
		{
			legAngle1 = legAngle1 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle2 = legAngle2 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle3 = legAngle3 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle4 = legAngle4 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
		}
	
		sleep 30;
	}
}


walk()
{
	var slowTurnSpd, slowTurnSpd2;

	slowTurnSpd = mATurnSpd2 / 4;
	slowTurnSpd2 = mATurnSpd2 / 6;
	
	
	// back contact point : right foot behind, left foot forward
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000>-legAngle4 speed mATurnSpd1;
		turn lleg to x-axis <0> speed mATurnSpd2;
		turn lfoot to x-axis <0> speed mATurnSpd1;
		//turn lthigh to x-axis <0.000000>-legAngle1 speed mATurnSpd1;
		//turn lleg to x-axis legAngle2 speed mATurnSpd2;
		//turn lfoot to x-axis legAngle3 speed mATurnSpd1;

		//turn rthigh to x-axis legAngle1 speed mATurnSpd2;
		turn rthigh to x-axis legAngle2 speed mATurnSpd2;
		turn rleg to x-axis legAngle3 speed mATurnSpd2;
		turn rfoot to x-axis <0.000000>-legAngle2 speed mATurnSpd1;

		// front legs, opposite
		turn ruparm to x-axis <0.000000>-legAngle1 speed mATurnSpd1;
		turn rloarm to x-axis legAngle2 speed mATurnSpd2;
		turn rhand to x-axis legAngle3 speed mATurnSpd1;

		turn luparm to x-axis legAngle2 speed mATurnSpd2;
		turn lloarm to x-axis legAngle3 speed mATurnSpd2;
		turn lhand to x-axis <0.000000>-legAngle2 speed mATurnSpd1;
		
		//move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		//move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		move pelvis to z-axis [2.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <4.000000> speed slowTurnSpd2;

		if(!bAiming)
		{
			// turn turret right, left arm forward
			//turn turret to y-axis <-10.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// passing pose 2 : right foot raising, left foot on the ground
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000> speed mATurnSpd2;
		turn lleg to x-axis <0.000000> speed mATurnSpd2;
		turn lfoot to x-axis <10.000000> speed mATurnSpd3;

		turn rthigh to x-axis <40.000000> speed mATurnSpd1;
		turn rleg to x-axis <-40.000000> speed mATurnSpd1;
		turn rfoot to x-axis <10.000000> speed mATurnSpd1;

		// front legs, opposite
		turn ruparm to x-axis <0.000000> speed mATurnSpd2;
		turn rloarm to x-axis <0.000000> speed mATurnSpd2;
		turn rhand to x-axis <10.000000> speed mATurnSpd3;

		turn luparm to x-axis <40.000000> speed mATurnSpd1;
		turn lloarm to x-axis <-40.000000> speed mATurnSpd1;
		turn lhand to x-axis <10.000000> speed mATurnSpd1;
		
				
		if(!bAiming)
		{
			// restore position
			turn turret to y-axis <0.000000> speed slowTurnSpd2;
		}

		sleep mADelay1;
		//move pelvis to y-axis [2.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}
	
	// forward contact point : right foot forward, left foot behind
	if( bMoving )
	{
		//turn lthigh to x-axis legAngle1 speed mATurnSpd2;
		turn lthigh to x-axis legAngle2 speed mATurnSpd2;
		turn lleg to x-axis legAngle3 speed mATurnSpd2;
		turn lfoot to x-axis <0>-legAngle2 speed mATurnSpd1;

		turn rthigh to x-axis <0>-legAngle4 speed mATurnSpd1;
		turn rleg to x-axis <0> speed mATurnSpd2;
		turn rfoot to x-axis <0> speed mATurnSpd1;
		//turn rthigh to x-axis <0>-legAngle1 speed mATurnSpd1;
		//turn rleg to x-axis legAngle2 speed mATurnSpd2;
		//turn rfoot to x-axis legAngle3 speed mATurnSpd1;
		
		
		// front legs, opposite
		turn ruparm to x-axis legAngle2 speed mATurnSpd2;
		turn rloarm to x-axis legAngle3 speed mATurnSpd2;
		turn rhand to x-axis <0>-legAngle2 speed mATurnSpd1;

		turn luparm to x-axis <0>-legAngle1 speed mATurnSpd1;
		turn lloarm to x-axis legAngle2 speed mATurnSpd2;
		turn lhand to x-axis legAngle3 speed mATurnSpd1;
				
		
		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		move pelvis to z-axis [2.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <4.000000> speed slowTurnSpd2;
		
		if(!bAiming)
		{
			// turn turret left, right arm forward
			//turn turret to y-axis <10.000000> speed slowTurnSpd2;
		}

		sleep mADelay1;
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// passing pose 1 : right foot on ground, left foot raising
	if( bMoving )
	{
		turn lthigh to x-axis <40.000000> speed mATurnSpd1;
		turn lleg to x-axis <-40.000000> speed mATurnSpd1;
		turn lfoot to x-axis <10.000000> speed mATurnSpd1;
		
		turn rthigh to x-axis <0.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <10.000000> speed mATurnSpd3;

		// front legs, opposite
		turn ruparm to x-axis <40.000000> speed mATurnSpd1;
		turn rloarm to x-axis <-40.000000> speed mATurnSpd1;
		turn rhand to x-axis <10.000000> speed mATurnSpd1;
		
		turn luparm to x-axis <0.000000> speed mATurnSpd2;
		turn lloarm to x-axis <0.000000> speed mATurnSpd2;
		turn lhand to x-axis <10.000000> speed mATurnSpd3;
		
		if(!bAiming)
		{
			// restore position
			turn turret to y-axis <0.000000> speed slowTurnSpd2;
		}
		
		sleep mADelay1;
		//move pelvis to y-axis [2.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}
	
	return;
}

// gallop-ish
run()
{
	var slowTurnSpd, slowTurnSpd2;

	slowTurnSpd = mATurnSpd2 / 2;
	slowTurnSpd2 = mATurnSpd2 / 6;
	
	// back impulse 
	if( bMoving )
	{
		turn lthigh to x-axis <40.000000> speed mATurnSpd2;
		turn lleg to x-axis <0> speed mATurnSpd2;
		turn lfoot to x-axis <20> speed mATurnSpd3;

		turn rthigh to x-axis <40.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <20.000000> speed mATurnSpd3;

		// front legs, opposite
		turn ruparm to x-axis <-35.000000> speed mATurnSpd2;
		turn rloarm to x-axis <0.000000> speed mATurnSpd2;
		turn rhand to x-axis <10.000000> speed mATurnSpd3;

		turn luparm to x-axis <-35.000000> speed mATurnSpd2;
		turn lloarm to x-axis <0.000000> speed mATurnSpd2;
		turn lhand to x-axis <10.000000> speed mATurnSpd3;
		
		//move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <-15.000000> speed slowTurnSpd;

		if(!bAiming)
		{
			// turn turret right, left arm forward
			//turn turret to y-axis <-10.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn lloarm around x-axis;
		wait-for-turn rloarm around x-axis;
		wait-for-turn pelvis around x-axis;
	}

	// passing pose 1
	if( bMoving )
	{
		turn lthigh to x-axis <20.000000> speed mATurnSpd2;
		turn lleg to x-axis <0> speed mATurnSpd2;
		turn lfoot to x-axis <0> speed mATurnSpd2;

		turn rthigh to x-axis <20.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <0.000000> speed mATurnSpd2;

		// front legs, opposite
		turn ruparm to x-axis <-35.000000> speed mATurnSpd2;
		turn rloarm to x-axis <0.000000> speed mATurnSpd2;
		turn rhand to x-axis <0.000000> speed mATurnSpd2;

		turn luparm to x-axis <-35.000000> speed mATurnSpd2;
		turn lloarm to x-axis <0.000000> speed mATurnSpd2;
		turn lhand to x-axis <0.000000> speed mATurnSpd2;
		
		//move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		move pelvis to y-axis [3.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd;

		if(!bAiming)
		{
			// turn turret right, left arm forward
			//turn turret to y-axis <-10.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn lloarm around x-axis;
		wait-for-turn rloarm around x-axis;
		wait-for-turn pelvis around x-axis;
	}

	// front land
	if( bMoving )
	{
		turn lthigh to x-axis <-25.000000> speed mATurnSpd2;
		turn lleg to x-axis <0> speed mATurnSpd3;
		turn lfoot to x-axis <0> speed mATurnSpd3;

		turn rthigh to x-axis <-25.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd3;
		turn rfoot to x-axis <0.000000> speed mATurnSpd3;

		// front legs, opposite
		turn ruparm to x-axis <-25.000000> speed mATurnSpd2;
		turn rloarm to x-axis <0.000000> speed mATurnSpd2;
		turn rhand to x-axis <0.000000> speed mATurnSpd2;

		turn luparm to x-axis <-25.000000> speed mATurnSpd2;
		turn lloarm to x-axis <0.000000> speed mATurnSpd2;
		turn lhand to x-axis <0.000000> speed mATurnSpd2;
		
		//move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		move pelvis to y-axis [2.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <10.000000> speed slowTurnSpd;

		if(!bAiming)
		{
			// turn turret right, left arm forward
			//turn turret to y-axis <-10.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn lloarm around x-axis;
		wait-for-turn rloarm around x-axis;
		wait-for-turn pelvis around x-axis;
	}

	// passing pose 2
	if( bMoving )
	{
		turn lthigh to x-axis <20.000000> speed mATurnSpd2;
		turn lleg to x-axis <0> speed mATurnSpd3;
		turn lfoot to x-axis <0> speed mATurnSpd3;

		turn rthigh to x-axis <20.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd3;
		turn rfoot to x-axis <0.000000> speed mATurnSpd3;

		// front legs, opposite
		turn ruparm to x-axis <10.000000> speed mATurnSpd2;
		turn rloarm to x-axis <0.000000> speed mATurnSpd3;
		turn rhand to x-axis <0.000000> speed mATurnSpd3;

		turn luparm to x-axis <10.000000> speed mATurnSpd2;
		turn lloarm to x-axis <0.000000> speed mATurnSpd3;
		turn lhand to x-axis <0.000000> speed mATurnSpd3;
		
		//move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		move pelvis to z-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd;

		if(!bAiming)
		{
			// turn turret right, left arm forward
			//turn turret to y-axis <-10.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn lloarm around x-axis;
		wait-for-turn rloarm around x-axis;
		wait-for-turn pelvis around x-axis;
	}
	
	return;
}


stand()
{
	move pelvis to y-axis [0.000000] speed [20.000000];
	turn pelvis to z-axis <0.000000> speed <95.000000> * SPEEDUP_FACTOR / 100;
	turn pelvis to x-axis <0.000000> speed <95.000000> * SPEEDUP_FACTOR / 100;
	turn front to z-axis <0.000000> speed <95.000000> * SPEEDUP_FACTOR / 100;
	
	turn rthigh to x-axis <10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn rleg to x-axis <0.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn rfoot to x-axis <-10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	
	turn lthigh to x-axis <10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn lleg to x-axis <0.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn lfoot to x-axis <-10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	
	turn luparm to x-axis <-10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn lloarm to x-axis <0.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn lhand to x-axis <10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	
	turn ruparm to x-axis <-10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn rloarm to x-axis <0.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	turn rhand to x-axis <10.000000> speed <360.000000> * SPEEDUP_FACTOR / 100;
	sleep 50;
}



MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			justmoved = TRUE;
			if (!shouldRun) {
				call-script walk();
			}
			if (shouldRun) {
				call-script run();
			}
		}
		if( !moving )
		{
			if( justMoved )
			{
				call-script stand();
				
				if( !aiming )
				{
					turn turret to y-axis <0.000000> speed <200.000000>;
				}

				justMoved = FALSE;
			}
			sleep 100;
		}
	}
	return (0);
}


SetMaxReloadTime(time)
{
	reloadTime = time;
	return (0);
}


ChargeControl(healthPercent, oldHealthPercent)
{
	set-signal-mask SIG_RESET;
	while(TRUE) {
		while( get BUILD_PERCENT_LEFT )
		{
			chargeState = [0];
			sleep 100;
		}
		
		while(( get TRANSPORT_ID ) > 0) {
			sleep 100;
		}		
		
		if (isReady == FALSE) {
			chargeState = [0];
			sleep reloadTime / 5;
			chargeState = [1];
			sleep reloadTime / 5;
			chargeState = [2];
			sleep reloadTime / 5;
			chargeState = [3];
			sleep reloadTime / 5;
			chargeState = [4];
			sleep reloadTime / 5;
			isReady = TRUE;
		}
		sleep 100;
	}
}

ChargeDisruptionControl(healthPercent, oldHealthPercent,oldHealthPercent2,oldHealthPercent3,oldHealthPercent4,oldHealthPercent5,oldHealthPercent6)
{
	oldHealthPercent = 100;
	oldHealthPercent2 = 100;
	oldHealthPercent3 = 100;
	oldHealthPercent4 = 100;
	oldHealthPercent5 = 100;
	oldHealthPercent6 = 100;
	
	
	while(TRUE) {
		while( get BUILD_PERCENT_LEFT )
		{
			sleep 100;
		}

		// taking 25% HP in damage or more within 3 seconds resets the charging process
		healthPercent = get HEALTH;
		
		if( (oldHealthPercent6 - healthpercent) > 25 || ( get TRANSPORT_ID ) > 0)
		{
			if (chargeState > [0] ) {
				signal SIG_RESET; // kill charge updater
				chargeState = [0];
				isReady = FALSE;
				set CEG_DAMAGE to 20;
				emit-sfx 1024+5 from flare;
				stop-spin ringholder around y-axis;
				hide ring1;
				hide ring2;
				hide ring3;
				hide ring4;
				call-script lua_resetReload();
				start-script ChargeControl(); // restore charge updater
			}
		}
		
		oldHealthPercent6 = oldHealthPercent5;
		oldHealthPercent5 = oldHealthPercent4;
		oldHealthPercent4 = oldHealthPercent3;
		oldHealthPercent3 = oldHealthPercent2;
		oldHealthPercent2 = oldHealthPercent;
		oldHealthPercent = healthPercent;
		sleep 500;
	}
}


ChargeFX(previousState) {

	while(TRUE) {
		
		if (chargeState == [0]) {
			if (previousState != chargeState) {
				stop-spin ringholder around y-axis;
				hide ring1;
				hide ring2;
				hide ring3;
				hide ring4;
			}
		}
		if (chargeState == [1]) {
			if (previousState != chargeState) {
				emit-sfx 1024+4 from flare;
				emit-sfx 1024+4 from ring1;
				emit-sfx 1024+4 from ring2;
				emit-sfx 1024+4 from ring3;
				emit-sfx 1024+4 from ring4;
				sleep 100;
				spin ringholder around y-axis speed <200>;
				show ring1;
				hide ring2;
				hide ring3;
				hide ring4;
				spin ring1 around y-axis speed <200>;
			}
			emit-sfx 1024 + 0 from ringfx;
		}
		if (chargeState == [2]) {
			if (previousState != chargeState) {
				spin ringholder around y-axis speed <400>;
				hide ring1;
				show ring2;
				hide ring3;
				hide ring4;
				spin ring2 around y-axis speed <400>;
			}
			emit-sfx 1024 + 1 from ringfx;
		}		
		if (chargeState == [3]) {
			if (previousState != chargeState) {
				spin ringholder around y-axis speed <600>;
				hide ring1;
				hide ring2;
				show ring3;
				hide ring4;
				spin ring3 around y-axis speed <600>;
			}
			emit-sfx 1024 + 2 from ringfx;
		}
		if (chargeState == [4]) {~
			if (previousState != chargeState) {
				spin ringholder around y-axis speed <800>;
				hide ring1;
				hide ring2;
				hide ring3;
				show ring4;
				spin ring4 around y-axis speed <800>;
			}
			emit-sfx 1024 + 3 from ringfx;
		}
		previousState = chargeState;
		sleep 30;
	}

}

resetReload() {
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 30;
	}
	call-script lua_resetReload();
}



SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from front;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}




Create()
{
	hide ring1;
	hide ring2;
	hide ring3;
	hide ring4;

	hide flare;
	bMoving = FALSE;
	bAiming = FALSE;
	shouldRun = FALSE;
	isReady = FALSE;
	chargeState = [0];
	reloadTime = 5000; // should be irrelevant

	//call-script stopAimHelper1(<1000>,<1000>);

	
	start-script MotionControl();
	start-script ChargeDisruptionControl();
	start-script ChargeControl();
	start-script ChargeFX();
	start-script SmokeUnit();
	start-script MotionSpeedControl();
	start-script resetReload();
	return (0);
}


StartMoving()
{
	bMoving = TRUE;
	return (0);
}

StopMoving()
{
	bMoving = FALSE;
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = front;
	return (0);
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	turn turret to y-axis <0.000000> speed <60.000000>;
	turn flare to x-axis <0.000000> now;
	bAiming = FALSE;
	return (0);
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	
	while (!isReady) {
		sleep 100;
	}
	turn turret to y-axis heading speed <940.000000>;
	turn flare to x-axis <0.000000> - pitch now;
	wait-for-turn turret around y-axis;

	start-script RestoreAfterDelay();
	return (1);
}


AimFromPrimary(piecenum)
{
	piecenum = turret;
	return (0);
}


FirePrimary() 
{
	chargeState = [0];
	hide ring1;
	hide ring2;
	hide ring3;
	hide ring4;
	isReady = FALSE;
	stop-spin ring1 around y-axis;
	stop-spin ring2 around y-axis;
	stop-spin ring3 around y-axis;
	stop-spin ring4 around y-axis;
	stop-spin ringholder around y-axis;
	turn ringholder to y-axis <0> now;
	emit-sfx 1024+4 from flare;
	return (1);
}


QueryPrimary(piecenum)
{
	piecenum = flare;
	return (0);
}


Killed(severity, corpsetype)
{
	if( severity <= 35 )
	{
		explode turret type SHATTER;
		explode ringholder type SHATTER;

		return (1);
	}
	explode ground type FALL | BITMAP3;
	explode lfoot type FALL | BITMAP4;
	explode lhand type FALL | BITMAP5;
	explode lleg type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode lloarm type FALL | BITMAP2;
	explode lthigh type FALL | BITMAP3;
	explode luparm type FALL | BITMAP4;
	explode rfoot type FALL | BITMAP1;
	explode rhand type FALL | BITMAP2;
	explode rleg type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode rloarm type FALL | BITMAP4;
	explode rthigh type FALL | BITMAP5;
	explode ruparm type FALL | BITMAP1;
	explode turret type SHATTER;
	explode ringholder type SHATTER;
	

	return (2);
}
