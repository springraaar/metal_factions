#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 140

piece  GP, base, torso, head, lleg1, lleg2, lleg3, rleg3, rleg2, rleg1, bodysect1, turretholder, rleg22, rleg33, rleg11, lleg33, lleg22, lleg11, rarm, rnano, rbeam, larm, lnano, lbeam;

static-var  bMoving,bAiming, nanopiece, justmoved, bBuilding,mASpdFactor;

// Signal definitions
#define SIG_AIM				2



MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = SPEEDUP_FACTOR;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

BuildFX()
{
	while(TRUE)
	{
		if( bBuilding )
		{
			emit-sfx 1024 + 0 from lbeam;
			emit-sfx 1024 + 0 from rbeam;
		}
		sleep 100;
	}
	return (0);
}

walk()
{
	if( bMoving )
	{
		turn rleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		
		turn base to x-axis <2.000000> speed <20.000000> * mASpdFactor / 100;

		turn lleg1 to y-axis <20.000000> speed <100.967033> * mASpdFactor / 100;
		turn lleg1 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn lleg2 to y-axis <-20.000000> speed <300.038462> * mASpdFactor / 100;
		turn lleg2 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lleg3 to y-axis <45.060440> speed <120.038462> * mASpdFactor / 100;
		turn lleg3 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg1 to y-axis <40.412088> speed <300.060440> * mASpdFactor / 100;
		turn rleg1 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rleg2 to y-axis <-20.093407> speed <100.368132> * mASpdFactor / 100;
		turn rleg2 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg3 to y-axis <15.137363> speed <300.681319> * mASpdFactor / 100;
		turn rleg3 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		
		sleep 175 * 100 / mASpdFactor;
	}

	// only vertical
	if( bMoving )
	{
		turn lleg2 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg1 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg3 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
		
		sleep 175 * 100 / mASpdFactor;
	}
	
	
	if( bMoving )
	{
		turn base to x-axis <-2.000000> speed <20.000000> * mASpdFactor / 100;

		turn lleg1 to y-axis <-40.000000> speed <300.967033> * mASpdFactor / 100;
		turn lleg1 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn lleg2 to y-axis <20.000000> speed <100.038462> * mASpdFactor / 100;
		turn lleg2 to z-axis <5.016484> speed <550.104396> * mASpdFactor / 100;

		turn lleg3 to y-axis <-15.060440> speed <300.038462> * mASpdFactor / 100;
		turn lleg3 to z-axis <-15.016484> speed <60.104396> * mASpdFactor / 100;

		turn rleg1 to y-axis <-20.412088> speed <100.060440> * mASpdFactor / 100;
		turn rleg1 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		turn rleg2 to y-axis <20.093407> speed <300.368132> * mASpdFactor / 100;
		turn rleg2 to z-axis <15.016484> speed <60.104396> * mASpdFactor / 100;
		
		turn rleg3 to y-axis <-45.137363> speed <120.681319> * mASpdFactor / 100;
		turn rleg3 to z-axis <-5.016484> speed <550.104396> * mASpdFactor / 100;
		
		sleep 175 * 100 / mASpdFactor;
	}
	
	// only vertical
	if( bMoving )
	{
		turn rleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;
		turn lleg22 to z-axis <0.000000> speed <80.000000> * mASpdFactor / 100;

		turn lleg1 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn lleg3 to z-axis <5.016484> speed <150.104396> * mASpdFactor / 100;
		turn rleg2 to z-axis <-5.016484> speed <150.104396> * mASpdFactor / 100;
	
		
		sleep 175 * 100 / mASpdFactor;
	}
	
}

Stop()
{
	turn base to z-axis <0.000000> speed <7.000000>;
	turn base to x-axis <0.000000> speed <7.000000>;
	turn lleg1 to y-axis <-25.000000> speed <237.000000>;
	turn lleg1 to z-axis <0.000000> speed <237.000000>;
	turn lleg2 to y-axis <0.000000> speed <237.000000>;
	turn lleg2 to z-axis <0.000000> speed <237.000000>;
	turn lleg3 to y-axis <0.000000> speed <237.000000>;
	turn lleg3 to z-axis <0.000000> speed <237.000000>;
	turn rleg3 to y-axis <0.000000> speed <237.000000>;
	turn rleg3 to z-axis <0.000000> speed <237.000000>;
	turn rleg2 to y-axis <0.000000> speed <237.000000>;
	turn rleg2 to z-axis <0.000000> speed <237.000000>;
	turn rleg1 to y-axis <25.000000> speed <237.000000>;
	turn rleg1 to z-axis <0.000000> speed <237.000000>;

}
MotionControl()
{
	while( TRUE )
	{
		if( bMoving )
		{
			justmoved=TRUE;
			call-script walk();
		}
		if( !bMoving )
		{
			if(justmoved)
			{
				call-script Stop();
				justmoved=FALSE;
			}
			sleep 100;
		}
	}
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

Create()
{
	
	bMoving = FALSE;
	justmoved = FALSE;
	bBuilding = FALSE;
	nanopiece=[0];
	turn lleg3 to y-axis <20.000000> speed <300.000000>;
	turn rleg3 to y-axis <-20.000000> speed <300.000000>;
	turn rleg1 to y-axis <30.000000> speed <300.000000>;
	turn lleg1 to y-axis <-30.000000> speed <300.000000>;
	start-script MotionSpeedControl();	
	start-script MotionControl();
	start-script SmokeUnit();
	start-script BuildFX();
	
}

RestoreAfterDelay()
{
	sleep DEFAULT_BUILDER_RESTORE_DELAY;
	turn torso to y-axis <0.000000> speed <100.000000>;
	wait-for-turn torso around y-axis;
	
}

QueryNanoPiece(piecenum)
{
	if( nanopiece == [0] )
	{
		piecenum = lbeam;
		nanopiece = [1];
		return (0);
	}
	if( nanopiece == [1] )
	{
		piecenum = rbeam;
		nanopiece = [0];
		return (0);
	}
}


StartBuilding(heading, pitch)
{
	bAiming = TRUE;
	signal SIG_AIM;
	turn torso to y-axis heading speed <300.071429>;
	wait-for-turn torso around y-axis;
	bBuilding = TRUE;
	set INBUILDSTANCE to 1;
}

StopBuilding()
{
	set INBUILDSTANCE to 0;
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bBuilding = FALSE;
	start-script RestoreAfterDelay();
}


SweetSpot(piecenum)
{
	piecenum = torso;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | BITMAP1;
		explode torso type BITMAPONLY | BITMAP3;
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | BITMAP1;
		explode torso type FALL | BITMAP3;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | BITMAP1;
		explode torso type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
		return (0);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | BITMAP1;
	explode torso type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
}
