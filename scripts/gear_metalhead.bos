#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 300

#define HEADING_TOLERANCE	<30>
#define PITCH_TOLERANCE		<20>

piece  ground, hips, leg1,bleg,leg2,bleg2,leg3,bleg3,leg4,bleg4, body, tbase, turret, sleeve, barrel, flare, cover, b2, legsup1, legsup2, legsup3, legsup4, top1, cover2, cover3, fcover1,fcover2;

static-var  bMoving, isOpen, bAiming;

lua_setMobility() { return 0; }

#include "aimhelper1.h"

// Signal definitions
#define SIG_MOVE			2
#define SIG_AIM				4
#define SIG_AIMNORESTORE	8

open()
{
	set ARMORED to FALSE;
	move tbase to y-axis [7.000000] speed [80.000000];
	move top1 to y-axis [11.500000] speed [80.000000];
	move cover2 to y-axis [5.500000] speed [80.000000];
	move cover3 to y-axis [5.500000] speed [80.000000];
	wait-for-move tbase along y-axis;
	wait-for-move top1 along y-axis;
	wait-for-move cover2 along y-axis;
	wait-for-move cover3 along y-axis;
	call-script lua_setMobility(100);
	sleep 100;
	isOpen = TRUE;
	return (0);
}

close()
{
	isOpen = FALSE;
	call-script lua_setMobility(20);

	move cover3 to y-axis [0.000000] speed [20.000000];
	move cover2 to y-axis [0.000000] speed [20.000000];
	move top1 to y-axis [0.000000] speed [20.000000];
	move tbase to y-axis [0.000000] speed [10.000000];
	wait-for-move tbase along y-axis;
	wait-for-move top1 along y-axis;
	wait-for-move cover2 along y-axis;
	wait-for-move cover3 along y-axis;
		
	set ARMORED to TRUE;
	isOpen = FALSE;
	return (0); 
}

walk()
{
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;
			
		/* Y */
		turn leg4 to y-axis <-80.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;

		turn leg2 to y-axis <-35.000000> speed <120.000000> * SPEEDUP_FACTOR / 100;

		turn leg3 to y-axis <5.000000> speed <140.000000> * SPEEDUP_FACTOR / 100;
		
		turn leg1 to y-axis <25.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;

		/* Z */
		turn bleg4 to z-axis <-20.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn bleg to z-axis <-20.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;

		/* move */
		move hips to x-axis [-0.2500000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		
		
		turn leg4 to z-axis <-20.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;
		turn leg4 to z-axis <0.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <1.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;
		
		/* Y */
		turn leg4 to y-axis <-35.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;

		turn leg2 to y-axis <-90.000000> speed <180.000000> * SPEEDUP_FACTOR / 100;

		turn leg3 to y-axis <15.000000> speed <140.000000> * SPEEDUP_FACTOR / 100;
		
		turn leg1 to y-axis <-5.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;

		/* Z */
		turn bleg4 to z-axis <0.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn bleg to z-axis <0.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;
		
		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		move hips to z-axis [0.000000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		
		turn leg1 to z-axis <-20.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;
		turn leg1 to z-axis <0.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;

		/* Y */
		turn leg4 to y-axis <-5.000000> speed <140.000000> * SPEEDUP_FACTOR / 100;

		turn leg2 to y-axis <-25.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;

		turn leg3 to y-axis <80.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		
		turn leg1 to y-axis <35.000000> speed <120.000000> * SPEEDUP_FACTOR / 100;

		/* Z */
		turn bleg2 to z-axis <20.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn bleg3 to z-axis <20.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn hips to z-axis <-3.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;


		/* move */
		move hips to x-axis [0.2500000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		
		turn leg3 to z-axis <20.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;
		turn leg3 to z-axis <0.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;

	}
	if( bMoving )
	{
		turn hips to x-axis <1.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;

		/* Y */
		turn leg4 to y-axis <-15.000000> speed <140.000000> * SPEEDUP_FACTOR / 100;

		turn leg2 to y-axis <5.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;

		turn leg3 to y-axis <35.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;

		turn leg1 to y-axis <90.000000> speed <180.000000> * SPEEDUP_FACTOR / 100;			

		/* Z */
		turn bleg3 to z-axis <0.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn bleg2 to z-axis <0.000000> speed <260.000000> * SPEEDUP_FACTOR / 100;
		turn hips to z-axis <3.000000> speed <20.000000> * SPEEDUP_FACTOR / 100;

		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * SPEEDUP_FACTOR / 100;
		move hips to z-axis [0.000000] speed [2.000000] * SPEEDUP_FACTOR / 100;

		turn leg2 to z-axis <20.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;
		turn leg2 to z-axis <0.000000> speed <200.000000> * SPEEDUP_FACTOR / 100;
		sleep 100 * 100 / SPEEDUP_FACTOR;
	}
}

MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		
		if( moving )
		{
			justmoved = TRUE;
			if( ! isOpen)
			{
				call-script open();
			}
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn leg1 to x-axis <0.000000> speed <360.000000>;
				turn leg2 to x-axis <0.000000> speed <360.000000>;
				turn leg3 to x-axis <0.000000> speed <360.000000>;
				turn leg4 to x-axis <0.000000> speed <360.000000>;
				turn leg1 to y-axis <45.000000> speed <360.000000>;
				turn leg2 to y-axis <-45.000000> speed <360.000000>;
				turn leg3 to y-axis <45.000000> speed <360.000000>;
				turn leg4 to y-axis <-45.000000> speed <360.000000>;
			
				turn leg1 to z-axis <0.000000> speed <360.000000>;
				turn leg2 to z-axis <0.000000> speed <360.000000>;
				turn leg3 to z-axis <0.000000> speed <360.000000>;
				turn leg4 to z-axis <0.000000> speed <360.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <360.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];

				turn bleg2 to z-axis <0.000000> speed <160.000000>;
				turn bleg4 to z-axis <0.000000> speed <160.000000>;
				turn bleg3 to z-axis <0.000000> speed <160.000000>;
				turn bleg to z-axis <0.000000> speed <160.000000>;
				justmoved = FALSE;
				if( !bAiming)
				{
					call-script close();
				}				
			}
			sleep 100;
		}
	}
	return (0);
}



SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from turret;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

StartMoving()
{
	bMoving = TRUE;
	return (0);
}

StopMoving()
{
	bMoving = FALSE;
	return (0);
}

RestoreAfterDelay()
{
	set-signal-mask SIG_AIMNORESTORE;
	bAiming = FALSE;
	if((get STANDINGFIREORDERS) > 0)
	{
		sleep DEFAULT_WEAPON_RESTORE_DELAY;
	} 
	else 
	{
		sleep 600;
	}
	call-script stopAimHelper1(<200>,<200>);
	turn turret to y-axis <0> speed <200.000000>;
	call-script close();
}
Create()
{
	isOpen = FALSE;
	bMoving = FALSE;
	bAiming = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionControl();
	start-script SmokeUnit();
	return (0);
}

SweetSpot(piecenum)
{
	piecenum = body;
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = flare;
	return (0);
}


AimPrimary(heading, pitch)
{
	signal SIG_AIMNORESTORE;
	
	// if it's not going to be able to fire on the next ~1s, don't try to aim
	if ((get WEAPON_RELOADSTATE(1)) > (get GAME_FRAME) + 20)) { return(0); }

	bAiming = TRUE;
	if( ! isOpen)
	{
		call-script open();
	}
	
	call-script updateAimHelper1(heading,pitch,<300>,<300>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;

	turn turret to y-axis heading speed <300.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	emit-sfx 1024 from flare;
	move fcover1 to y-axis [-2.8] now;
	move fcover2 to y-axis [2.8] now;
	sleep 400;
	move fcover1 to y-axis [0] speed [16];
	move fcover2 to y-axis [0] speed [16];
	set-signal-mask SIG_AIM;
	sleep 800;
	if (!bMoving) 
	{
		call-script close();
	}
	return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode cover type SHATTER | BITMAP2;
		explode legsup1 type SHATTER | BITMAP2;
		explode legsup2 type SHATTER | BITMAP2;
		explode legsup3 type SHATTER | BITMAP2;
		explode legsup4 type SHATTER | BITMAP2;
		return (1);
	}
	explode body type SHATTER | BITMAP2;
	explode turret type SHATTER | BITMAP2;
	explode b2 type SHATTER | BITMAP2;	
	explode cover type SHATTER | BITMAP2;
	explode legsup1 type SHATTER | BITMAP2;
	explode legsup2 type SHATTER | BITMAP2;
	explode legsup3 type SHATTER | BITMAP2;
	explode legsup4 type SHATTER | BITMAP2;
	explode leg1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg3 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	explode leg4 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
	return (2);
}
