#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 	100

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  gp, base, turret, sup,holder, sleeve, lbarrel, lflash, rbarrel, rflash, base2,l1,l2,l3,l4,t1,t2,t1cover,t2cover;

static-var  gun_1;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2

#include "tankthreads.h"

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}



Create()
{
	gun_1=0;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script SmokeUnit();
	start-script AnimateTracks();
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<90>,<90>);
	turn turret to y-axis <0.000000> speed <90.016484>;
	turn sleeve to x-axis <0.000000> speed <90.005495>;
}

AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<400>,<400>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <400.038462>;
	turn sleeve to x-axis <0.000000> - pitch speed <400.000000>;
	
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

Shot1()
{
	if(gun_1 == 0)
	{
		emit-sfx 1024 from lflash;
		sleep 100;
		gun_1 = 1;
	}
	else
	{
		emit-sfx 1024 from rflash;
		sleep 100;
		gun_1 = 0;
		return (0);
	}	
	
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if( gun_1 == 0)
	{
		piecenum = lflash;
	}
	else
	{
		piecenum = rflash;
	}
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode turret type SHATTER;
		explode lbarrel type SHATTER;
		explode rbarrel type SHATTER;
		return (1);
	}
	explode turret type SHATTER;
	explode lbarrel type SHATTER;
	explode rbarrel type SHATTER;
	explode t1cover type SHATTER;
	explode t2cover type SHATTER;
	return (2);
}
