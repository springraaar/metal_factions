#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR	150

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  flare1, flare2, torso, hips, lthigh, rthigh, rleg, lleg, rfoot, head, luparm, lgunbase, lgunshaft, ruparm, rgunbase, rgunshaft, rshoulder, lshoulder,
       larm, rarm, lfoot, lgunbarrel, rgunbarrel;

static-var  gun_1, bMoving, bAiming, mATurnSpd1,mATurnSpd2,mATurnSpd3,mASpdFactor, mAMoveSpd1, mAVertFactor, isOpen, legAngle1, legAngle2, legAngle3;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2


MotionSpeedControl(maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;

	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < 40 )
		{
			mASpdFactor = 40;
		}
		
		
		mAMoveSpd1 = [20.000000] * mASpdFactor / 100;
		mATurnSpd1 = <315.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd2 = <200.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd3 = <50.000000> * (100 + mASpdFactor) / 200;

		mAVertFactor = [0.000000]-[3.000000] * mASpdFactor / SPEEDUP_FACTOR;
		legAngle1 = <50.000000>;
		legAngle2 = <30.000000>;
		legAngle3 = <20.000000>;
		if ( mASpdFactor < SPEEDUP_FACTOR )
		{
			legAngle1 = legAngle1 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle2 = legAngle2 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle3 = legAngle3 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
		}
	
		sleep 60;
	}
}


walk()
{
	var slowTurnSpd, slowTurnSpd2;

	slowTurnSpd = mATurnSpd2 / 2;
	slowTurnSpd2 = mATurnSpd2 / 6;
	
	
	// back contact point : right foot behind, left foot forward
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000>-legAngle1 speed mATurnSpd1;
		turn lleg to x-axis legAngle2 speed mATurnSpd2;
		turn lfoot to x-axis <0> speed mATurnSpd3;

		turn rthigh to x-axis legAngle2 speed mATurnSpd2;
		turn rleg to x-axis legAngle3 speed mATurnSpd2;
		turn rfoot to x-axis <0.000000>-legAngle2 speed mATurnSpd1;

		move hips to y-axis mAVertFactor speed mAMoveSpd1;
		turn hips to x-axis <6.000000> speed slowTurnSpd2;
		if(!bAiming)
		{
			// turn torso left, right arm forward
			turn torso to y-axis <10.000000> speed slowTurnSpd2;
			turn lshoulder to x-axis <10.000000> speed slowTurnSpd;
			turn larm to x-axis <-10.000000> speed slowTurnSpd2;

			turn rshoulder to x-axis <-40.000000> speed slowTurnSpd;
			turn rarm to x-axis <-20.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 2 : left stomp ground
	if( bMoving)
	{
		turn lfoot to x-axis legAngle3 speed mATurnSpd1*3;
		wait-for-turn lfoot around x-axis;
	}

	// passing pose 2 : right foot raising, left foot on the ground
	if( bMoving )
	{
		turn lthigh to x-axis <0.000000> speed mATurnSpd2;
		turn lleg to x-axis <0.000000> speed mATurnSpd2;
		turn lfoot to x-axis <0.000000> speed mATurnSpd3;

		turn rthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn rleg to x-axis <30.000000> speed mATurnSpd2;
		turn rfoot to x-axis <10.000000> speed mATurnSpd1;

		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn lshoulder to x-axis <-15.000000> speed slowTurnSpd;
			turn larm to x-axis <-15.000000> speed slowTurnSpd2;
			turn rshoulder to x-axis <-15.000000> speed slowTurnSpd;
			turn rarm to x-axis <-15.000000> speed slowTurnSpd2;

		}

		move hips to y-axis [0.000000] speed mAMoveSpd1;
		turn hips to x-axis <0.000000> speed slowTurnSpd2;		
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;

	}
	
	// forward contact point : right foot forward, left foot behind
	if( bMoving )
	{
		turn lthigh to x-axis legAngle2 speed mATurnSpd2;
		turn lleg to x-axis legAngle3 speed mATurnSpd2;
		turn lfoot to x-axis <0>-legAngle2 speed mATurnSpd1;

		turn rthigh to x-axis <0>-legAngle1 speed mATurnSpd1;
		turn rleg to x-axis legAngle2 speed mATurnSpd2;
		turn rfoot to x-axis <0> speed mATurnSpd3;
		
		move hips to y-axis mAVertFactor speed mAMoveSpd1;
		turn hips to x-axis <6.000000> speed slowTurnSpd2;
		
		if(!bAiming)
		{
			// turn torso right, left arm forward
			turn torso to y-axis <-10.000000> speed slowTurnSpd2;
			turn lshoulder to x-axis <-40.000000> speed slowTurnSpd;
			turn larm to x-axis <-20.000000> speed slowTurnSpd2;
			turn rshoulder to x-axis <10.000000> speed slowTurnSpd;
			turn rarm to x-axis <-10.000000> speed slowTurnSpd2;
		}

		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 1 : right stomp ground
	if( bMoving)
	{
		turn rfoot to x-axis legAngle3 speed mATurnSpd1*3;
		wait-for-turn rfoot around x-axis;
	}

	// passing pose 1 : right foot on ground, left foot raising
	if( bMoving )
	{
		turn lthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn lleg to x-axis <30.000000> speed mATurnSpd2;
		turn lfoot to x-axis <10.000000> speed mATurnSpd1;
		
		turn rthigh to x-axis <0.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <0.000000> speed mATurnSpd3;

		
		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn lshoulder to x-axis <-15.000000> speed slowTurnSpd;
			turn larm to x-axis <-15.000000> speed slowTurnSpd2;
			turn rshoulder to x-axis <-15.000000> speed slowTurnSpd;
			turn rarm to x-axis <-15.000000> speed slowTurnSpd2;
		}
		
		move hips to y-axis [0.000000] speed mAMoveSpd1;
		turn hips to x-axis <0.000000> speed slowTurnSpd2;
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}
	
	return;
}


Open()
{
	move lgunshaft to x-axis [0.000000] speed [60.000000];
	move rgunshaft to x-axis [0.000000] speed [60.000000];
	//turn luparm to y-axis <-70.000000> speed <420.000000>;
	//turn ruparm to y-axis <70.000000> speed <420.000000>;
	turn larm to x-axis <0.000000> speed <620.000000>;
	turn rarm to x-axis <0.000000> speed <620.000000>;
	wait-for-move lgunshaft along x-axis;
	wait-for-move rgunshaft along x-axis;
	move lgunbarrel to y-axis [0.000000] speed [100.000000];
	move rgunbarrel to y-axis [0.000000] speed [100.000000];
	wait-for-move lgunbarrel along y-axis;
	wait-for-move rgunbarrel along y-axis;
	wait-for-turn larm around x-axis;
	wait-for-turn rarm around x-axis;
	//wait-for-turn luparm around y-axis;
	//wait-for-turn ruparm around y-axis;
	isOpen = TRUE;
	return (0);
}

Close()
{
	isOpen = FALSE;

	turn luparm to y-axis <0.000000> speed <220.000000>;
	turn ruparm to y-axis <0.000000> speed <220.000000>;
	move lgunbarrel to y-axis [10.000000] speed [20.000000];
	move rgunbarrel to y-axis [10.000000] speed [20.000000];
	wait-for-move lgunbarrel along y-axis;
	wait-for-move rgunbarrel along y-axis;
	wait-for-turn luparm around y-axis;
	wait-for-turn ruparm around y-axis;

	move lgunshaft to x-axis [3.000000] speed [20.000000];
	move rgunshaft to x-axis [-3.000000] speed [20.000000];
	wait-for-move lgunshaft along x-axis;
	wait-for-move rgunshaft along x-axis;


	sleep 100;	
	return (0);
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}


MotionControl(moving, aiming, justmoved)
{
	justmoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			if( aiming )
			{
				call-script walk();
			}
			if( !aiming )
			{
				call-script walk();
			}
			justmoved = TRUE;
		}
		if( !moving )
		{
			if( justMoved )
			{
				move hips to y-axis [0.000000] speed [25.000000];
				turn hips to x-axis <0.000000> speed <20.000000>;
				turn rthigh to x-axis <0.000000> speed <200.000000>;
				turn rleg to x-axis <0.000000> speed <200.000000>;
				turn lthigh to x-axis <0.000000> speed <200.000000>;
				turn lleg to x-axis <0.000000> speed <200.000000>;
				turn rfoot to x-axis <0.000000> speed <200.000000>;
				turn lfoot to x-axis <0.000000> speed <200.000000>;

				if( !aiming )
				{
					turn torso to y-axis <0.000000> speed <200.000000>;
					turn lshoulder to x-axis <-10.000000> speed <200.000000>;
					turn rshoulder to x-axis <-10.000000> speed <200.000000>;
					turn larm to x-axis <-20.000000> speed <200.000000>;
					turn rarm to x-axis <-20.000000> speed <200.000000>;
				}

				justMoved = FALSE;
			}
			sleep 100;
		}
	}
	return (0);
}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	gun_1 = flare1;
	isOpen = FALSE;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionControl();
	start-script SmokeUnit();
	start-script MotionSpeedControl();
	call-script Close();
	return (0);
}

StartMoving()
{
	bMoving = TRUE;
	return (0);
}

StopMoving()
{
	bMoving = FALSE;
	return (0);
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY2;
	call-script stopAimHelper1(<1000>,<1000>);
	turn rshoulder to x-axis <0.000000> speed <350.027473>;
	turn lshoulder to x-axis <0.000000> speed <350.027473>;
	wait-for-turn rshoulder around x-axis;
	wait-for-turn lshoulder around x-axis;

	turn torso to y-axis <0.000000> speed <200.038462>;

	start-script Close();
	bAiming = FALSE;
	return (0);
}

AimPrimary(heading, pitch)
{
	bAiming = TRUE;

	turn torso to y-axis heading speed <350.027473>;
	turn rshoulder to x-axis <-90.000000> - pitch speed <350.027473>;
	turn lshoulder to x-axis <-90.000000> - pitch speed <350.027473>;
	if ( ! isOpen )
	{
		call-script Open();
	}

	call-script updateAimHelper1(heading,pitch + <90>,<350>,<350>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	
	if (!aimReady1)
	{
		wait-for-turn rshoulder around x-axis;
		wait-for-turn lshoulder around x-axis;
		wait-for-turn torso around y-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if( gun_1 == 0 )
	{
		move rgunbarrel to y-axis [2.800000] now;
		move ruparm to y-axis [1.500000] now;
		emit-sfx 1024 + 0 from flare1;
		sleep 150;
		move rgunbarrel to y-axis [0.000000] speed [37.500000];
		move ruparm to y-axis [0.000000] speed [37.500000];
	}
	if( gun_1 == 1 )
	{
		move lgunbarrel to y-axis [2.800000] now;
		move luparm to y-axis [1.500000] now;
		
		emit-sfx 1024 + 0 from flare2;
		sleep 150;
		move lgunbarrel to y-axis [0.000000] speed [37.500000];
		move luparm to y-axis [0.000000] speed [37.500000];
	}
	gun_1 = !gun_1;
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
	return (0);
}

QueryPrimary(piecenum)
{
	piecenum = gun_1;
	return (0);
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode torso type SHATTER | EXPLODE_ON_HIT | BITMAP4;
		return (1);
	}

	explode luparm type SHATTER;
	explode ruparm type SHATTER;
	explode lthigh type SHATTER;
	explode rthigh type SHATTER;
	explode torso type SHATTER | EXPLODE_ON_HIT | BITMAP4;
	return (2);
}
