#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, blades, post;

static-var  windSpeed, windAngle;


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

SetSpeed(spd)
{
	windSpeed = spd;
}

SetDirection(angle)
{
	windAngle = angle;
}


activationControl(buildPercentLeft)
{
	while(TRUE)
	{
		buildPercentLeft = get(BUILD_PERCENT_LEFT);
		if ( buildPercentLeft == 0 && windSpeed > 0)
		{
			spin blades around z-axis speed windSpeed;
			turn post to y-axis windAngle speed <60.000000>;
		}
		else
		{
			stop-spin blades around z-axis decelerate <10.000000>;
			stop-spin post around y-axis decelerate <10.000000>;
		}
	
		sleep 100;
	}
}


Create()
{
	windSpeed = 0;
	windAngle = 0;
	start-script SmokeUnit();
	start-script activationControl();
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode post type SHATTER;
		explode blades type SHATTER;
		return (1);
	}
	explode base type SHATTER;
	explode post type SHATTER;
	explode blades type SHATTER;
	return (2);
}
