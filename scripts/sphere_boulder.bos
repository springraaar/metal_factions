#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 110

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<25>

piece  groundx2, hips, torso,tl,tr,sleeve,b1, b2, barrel1,barrel2,flare1,flare2, s2,leg1, bleg, leg2, bleg2, leg3, bleg3, leg4, bleg4;

static-var  bMoving, bAiming, justmoved, gun_1, mASpdFactor;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}

walk()
{
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <10.000000> * mASpdFactor / 100;
			
		/* Y */
		turn leg4 to y-axis <-80.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-35.000000> speed <120.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <5.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <25.000000> speed <260.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <10.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		
		
		turn leg4 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg4 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <1.000000> speed <10.000000> * mASpdFactor / 100;
		
		/* Y */
		turn leg4 to y-axis <-35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-90.000000> speed <180.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <15.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <-5.000000> speed <200.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <10.000000> * mASpdFactor / 100;
		
		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		
		turn leg1 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg1 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <10.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-5.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-25.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <80.000000> speed <260.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <35.000000> speed <120.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg2 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg3 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <-1.000000> speed <10.000000> * mASpdFactor / 100;


		/* move */
		move hips to x-axis [0.2500000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		
		turn leg3 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg3 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		turn hips to x-axis <1.000000> speed <10.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-15.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <5.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg1 to y-axis <90.000000> speed <180.000000> * mASpdFactor / 100;			

		/* Z */
		turn bleg3 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg2 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <10.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [2.000000] * mASpdFactor / 100;

		turn leg2 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg2 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
	}
}


MotionControl(moving)
{
	while( TRUE )
	{
		moving = bMoving;
		if( moving )
		{
			justmoved = TRUE;
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn leg1 to x-axis <0.000000> speed <360.000000>;
				turn leg2 to x-axis <0.000000> speed <360.000000>;
				turn leg3 to x-axis <0.000000> speed <360.000000>;
				turn leg4 to x-axis <0.000000> speed <360.000000>;
				turn leg1 to y-axis <45.000000> speed <360.000000>;
				turn leg2 to y-axis <-45.000000> speed <360.000000>;
				turn leg3 to y-axis <45.000000> speed <360.000000>;
				turn leg4 to y-axis <-45.000000> speed <360.000000>;
			
				turn leg1 to z-axis <0.000000> speed <360.000000>;
				turn leg2 to z-axis <0.000000> speed <360.000000>;
				turn leg3 to z-axis <0.000000> speed <360.000000>;
				turn leg4 to z-axis <0.000000> speed <360.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <360.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];

				turn bleg2 to z-axis <0.000000> speed <160.000000>;
				turn bleg4 to z-axis <0.000000> speed <160.000000>;
				turn bleg3 to z-axis <0.000000> speed <160.000000>;
				turn bleg to z-axis <0.000000> speed <160.000000>;

				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	justmoved=FALSE;
	gun_1 = 0;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
}

StartMoving()
{
	bMoving = TRUE;
}

StopMoving()
{
	bMoving = FALSE;
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<100>,<100>);
	turn torso to y-axis <0.000000> speed <100>;
	turn sleeve to x-axis <0.000000> speed <100>;
	bAiming = FALSE;
}


AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<150>,<150>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;
	turn torso to y-axis heading speed <150.027473>;
	turn sleeve to x-axis <0.000000> - pitch speed <150.038462>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}


Shot1()
{
	if (gun_1 == 0)
	{
		move barrel1 to z-axis [-3] now;
		emit-sfx 1024 + 0 from flare1;
		gun_1 = 1;
		move barrel1 to z-axis [0] speed [10];
		return (0);
	}
	if (gun_1 == 1)
	{
		move barrel2 to z-axis [-3] now;
		emit-sfx 1024 + 0 from flare2;
		gun_1 = 0;
		move barrel2 to z-axis [0] speed [10];
		return (0);
	}
}

QueryPrimary(piecenum)
{
	if (gun_1 == 0)
	{
		piecenum = flare1;
		return (0);
	}
	if (gun_1 == 1)
	{
		piecenum = flare2;
		return (0);
	}
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
}


Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode b1 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
		explode b2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
		explode s2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
	
		return (1);
	}

	explode b1 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
	explode b2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;
	explode s2 type SHATTER | EXPLODE_ON_HIT | BITMAP5;

	explode tl type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode leg4 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
	explode tr type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;

	explode leg3 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP4;
	explode torso type SHATTER | EXPLODE_ON_HIT | BITMAP5;~
	return (2);
}
