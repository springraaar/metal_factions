#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turbase, turret, sleeve, llauncher, lflare,rlauncher,rflare,r1,r2,r3,l1,l2,l3, b1,b2,b3,b4,ldoor,rdoor,bottom,aimpt,floatbase;

static-var  gun_1,canAim,isFloating;

// Signal definitions
#define SIG_AIM				2

lua_stopFiring() { return 0; }
lua_checkAllowFiring() { return 0; }
lua_checkLockTarget() { return 0; }
lua_checkIncomingMissile() { return 0; }

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Open()
{
	SET ARMORED to FALSE;

	turn ldoor to z-axis <90.000000> speed <450.000000>;
	turn rdoor to z-axis <-90.000000> speed <450.000000>;
	wait-for-turn ldoor around z-axis;
	wait-for-turn rdoor around z-axis;

	move ldoor to y-axis [-25] speed [200];
	move rdoor to y-axis [-25] speed [200];
	move turbase to y-axis [35] speed [170];
	wait-for-move turbase along y-axis;
	wait-for-move ldoor along y-axis;
	wait-for-move rdoor along y-axis;

	canAim = TRUE;
}


Close()
{
	canAim = FALSE;

	turn sleeve to x-axis <0.000000> speed <300.000000>;
	turn turret to y-axis <0.000000> speed <300.000000>;
	move turbase to y-axis [0] speed [35];
	wait-for-turn sleeve around x-axis;
	wait-for-turn turret around y-axis;
	wait-for-move turbase along y-axis;

	move ldoor to y-axis [0] speed [50];
	move rdoor to y-axis [0] speed [50];
	wait-for-move ldoor along y-axis;
	wait-for-move rdoor along y-axis;
	
	turn ldoor to z-axis <0.000000> speed <300.000000>;
	turn rdoor to z-axis <0.000000> speed <300.000000>;
	wait-for-turn ldoor around z-axis;
	wait-for-turn rdoor around z-axis;
	
	SET ARMORED to TRUE;
}


RestoreAfterDelay()
{
	if((get STANDINGFIREORDERS) > 0)
	{
		sleep DEFAULT_WEAPON_RESTORE_DELAY;
	} 
	else 
	{
		sleep 600;
	}
	
	call-script Close();
}

// if fire state changes to hold fire, force closure
checkClose(fireState,previousFireState)
{
	while (TRUE)
	{
		fireState = (get STANDINGFIREORDERS);
		
		if (fireState == 0 && previousFireState > 0) 
		{
			signal SIG_AIM;
			call-script lua_stopFiring();
			call-script RestoreAfterDelay();
		}
	
		previousFireState = fireState;
		sleep 100;
	}
}

SweetSpot(piecenum)
{
	piecenum = turret;
}

AimFromPrimary(piecenum)
{
	piecenum = aimpt;
}

AimPrimary(heading, pitch, canFire)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	if (canAim == FALSE)
	{
		call-script Open();
	}
	turn sleeve to x-axis <0.000000> - pitch speed <300.000000>;
	turn turret to y-axis heading speed <300.000000>;
	wait-for-turn sleeve around x-axis;
	wait-for-turn turret around y-axis;
	
	start-script RestoreAfterDelay();
	
	// overkill prevention check
	canFire = (get GAME_FRAME) - (get WEAPON_RELOADSTATE(1)) > 0;
	if (canFire) {
		call-script lua_checkAllowFiring(1,(get TARGET_ID(1)));
		//get PRINT(get LUA0,get LUA1, get LUA2)
		canFire = get LUA1;
	}
	if (!canFire) {
		return (0);
	}
	
	return (1);
}


checkIncomingMissile(fireState, needToOpen) {
	needToOpen = FALSE;
	while (TRUE) {
		fireState = (get STANDINGFIREORDERS);
		
		if (fireState > 0) {
			call-script lua_checkIncomingMissile(1900);
			needToOpen = get LUA1;
			if (needToOpen == 1) {
				start-script AimPrimary(<0>,<0>);
				sleep DEFAULT_WEAPON_RESTORE_DELAY;
			}
		}
		sleep 500;
	}
}

Create()
{
	hide floatbase;
	isFloating = FALSE;
	if ( GET GROUND_WATER_HEIGHT(GET UNIT_XZ) < [0])
	{
		isFloating = TRUE;
		show floatbase;
		move base to y-axis [2] now;
	}
	
	hide lflare;
	hide rflare;
	move aimpt to y-axis [40.000000] now;
	gun_1=[0];
	canAim = FALSE;
	start-script SmokeUnit();
	start-script Close();
	start-script checkClose();
	start-script checkIncomingMissile();
}

FirePrimary()
{
	call-script lua_checkLockTarget(get TARGET_ID(1));
	if (gun_1 == [0])
	{
		emit-sfx 1024+0 from lflare;
		sleep 100;
		gun_1 = [1];
		return(1);
	}
	if (gun_1 == [1])
	{
		emit-sfx 1024+0 from rflare;
		sleep 100;
		gun_1 = [0];
		return(1);
	}
}

QueryPrimary(piecenum)
{
	if (gun_1 == [0])
	{
		piecenum = lflare;
	}
	if (gun_1 == [1])
	{
		piecenum = rflare;
	}
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		if (isFloating) 
		{
			corpsetype = 1;
		}
		if (!isFloating) 
		{
			corpsetype = 2;
		}
		explode turret type SHATTER;
		explode llauncher type SHATTER;
		explode rlauncher type SHATTER;
		return corpsetype;
	}

	explode base type SHATTER;
	explode ldoor type SHATTER;
	explode rdoor type SHATTER;
	explode turbase type SHATTER;
	explode turret type SHATTER;
	explode sleeve type SHATTER;
	explode llauncher type SHATTER;
	explode rlauncher type SHATTER;
	return (3);
}
