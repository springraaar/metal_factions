#define TAK			// This is a TAK script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 110
#define RESTORE_DELAY	3000

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  torso, ruparm, luparm, larm, rarm, lflare,rflare, pelvis, rnano,rnanospray, lnano, lnanospray, lgun,lbg2,lbg3,lbg4,lbg5,lfirept,ltorpgun,ltorpedo,rgun,rbg2,rbg3,rbg4,rbg5,rfirept,rtorpgun,rtorpedo, head, h2, h3, pack, lthigh, rthigh, rleg, lleg, lfoot, rfoot,jumpback,jbl,jbr,lbjetpt,rbjetpt,jumpjets,lfjet,rfjet,lfjetpt,rfjetpt;

static-var  bMoving, bBuilding, bAiming, Static_Var_6, bDgunning, bUnderwater, canAim1, canAim2, gun_1,gun_2,nano1,mATurnSpd1,mATurnSpd2,mATurnSpd3,mASpdFactor, mAMoveSpd1, mAVertFactor, legAngle1, legAngle2, legAngle3;

#include "aimhelper1.h"

// signal definition
#define SIG_AIM		2

lua_delayReload() { return 0; }


#include "jumpvars.h"


showJumpGear() {
	show jumpback;
	show jbl;
	show jbr;
	show jumpjets;
	show lfjet;
	show rfjet;
}

hideJumpGear() {
	hide jumpback;
	hide jbl;
	hide jbr;
	hide jumpjets;
	hide lfjet;
	hide rfjet;
}

showJumpEffects(descending, frames) {
	if (descending) {
		emit-sfx 1024+6 from lfjetpt;
		emit-sfx 1024+6 from rfjetpt;
		emit-sfx 1024+5 from lbjetpt;
		emit-sfx 1024+5 from rbjetpt;
		if (frames == JUMP_SND_FRAMES) {
			play-sound( "jumpdesc", 1 );
		}
	} else {
		if (frames == 0 ) {
			play-sound( "jumpstart", 1 );
		} else {
			if (frames == JUMP_SND_FRAMES) {
				play-sound( "jumpasc", 1 );
			}
		}
		emit-sfx 1024+4 from lfjetpt;
		emit-sfx 1024+4 from rfjetpt;
		emit-sfx 1024+3 from lbjetpt;
		emit-sfx 1024+3 from rbjetpt;
	}
}

#include "jump.h"

MotionSpeedControl(maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		//call-script lua_cobDebug(mASpdFactor);
		if ( mASpdFactor < SPEEDUP_FACTOR/2 )
		{
			mASpdFactor = SPEEDUP_FACTOR/2;
		}
		
		
		mAMoveSpd1 = [20.000000] * mASpdFactor / 100;
		mATurnSpd1 = <315.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd2 = <200.000000> * (100 + mASpdFactor) / 200;
		mATurnSpd3 = <50.000000> * (100 + mASpdFactor) / 200;

		mAVertFactor = [0.000000]-[1.000000] * mASpdFactor / SPEEDUP_FACTOR;
		legAngle1 = <40.000000>;
		legAngle2 = <30.000000>;
		legAngle3 = <15.000000>;
		if ( mASpdFactor < SPEEDUP_FACTOR )
		{
			legAngle1 = legAngle1 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle2 = legAngle2 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);
			legAngle3 = legAngle3 * (SPEEDUP_FACTOR + mASpdFactor) / (2*SPEEDUP_FACTOR);

		}
	
		sleep 60;
	}
}

Sing()
{
	Static_Var_6 = Rand( 0, 9 );
	if( Static_Var_6 == 0 )
	{
		play-sound( "honk", 15 );
		play-sound( "honk", 15 );
		play-sound( "honk", 15 );
		play-sound( "honk", 15 );
		play-sound( "honk", 15 );
		return (0);
	}
	if( Static_Var_6 == 1 )
	{
		play-sound( "CorCom1", 15 );
		play-sound( "CorCom1", 15 );
		play-sound( "CorCom1", 15 );
		play-sound( "CorCom1", 15 );
		play-sound( "CorCom1", 15 );
		return (0);
	}
	if( Static_Var_6 > 1 AND Static_Var_6 < 10 )
	{
		play-sound( "sing", 15 );
		play-sound( "sing", 15 );
		play-sound( "sing", 15 );
		play-sound( "sing", 15 );
		play-sound( "sing", 15 );
		return (0);
	}
}




// shows nano effect while building
Build()
{
	while(TRUE)
	{
		if( bBuilding )
		{
			emit-sfx 1024 + 1 from lnanospray;
			emit-sfx 1024 + 1 from rnanospray;
			call-script lua_delayReload(3);
		}
		sleep 100;
	}
	return;
}

checkUnderwater()
{
	var currentHeight, oldState, i;
	while(TRUE)
	{   
 		currentHeight = GET GROUND_WATER_HEIGHT(GET UNIT_XZ);
 		oldState = bUnderwater;
		
		// check current state
		if ( currentHeight < [-30])
 		{
 			bUnderwater = TRUE;
		
 		}
 		if (currentHeight >= [-30])
 		{
 			bUnderwater = FALSE;
 		}
 		
 		// handle state changes
 		if (oldState != bUnderwater)
 		{
			if(bUnderwater)
			{
				canAim1 = FALSE;
				//hide cannon stuff
				hide lbg3;
				hide rbg3;
 				
				// effects
				i=0;
				while(i < 5)
				{
					emit-sfx 1024+7 from lbg3;
					emit-sfx 1024+7 from rbg3;
					i=i+1;
					sleep 100;
				}
				
				//show torpedo stuff 			
 				move ltorpedo to z-axis [0.000000] now;
 				show ltorpedo;
				show ltorpgun;
 				move rtorpedo to z-axis [0.000000] now;
 				show rtorpedo;
				show rtorpgun;

				// effects
				i=0;
				while(i < 5)
				{
					emit-sfx 1024+7 from ltorpgun;
					emit-sfx 1024+7 from rtorpgun;
					i=i+1;
					sleep 100;
				}

 				canAim2 = TRUE;
			}
			if(!bUnderwater)
			{
 				canAim2 = FALSE;
				//hide torpedo stuff
				move ltorpedo to z-axis [0.000000] now;
 				hide ltorpedo;
				hide ltorpgun;
 				move rtorpedo to z-axis [0.000000] now;
 				hide rtorpedo;
				hide rtorpgun;

				// effects
				i=0;
				while(i < 5)
				{
					emit-sfx 1024+7 from ltorpgun;
					emit-sfx 1024+7 from rtorpgun;
					i=i+1;
					sleep 100;
				}

				//show cannon stuff 			
 				show lbg3;
 				show rbg3;

				// effects
				i=0;
				while(i < 5)
				{
					emit-sfx 1024+7 from lbg3;
					emit-sfx 1024+7 from rbg3;
					i=i+1;
					sleep 100;
				}
 		
 				canAim1 = TRUE;
			}
 		}
 		
 		sleep 500;
	}
	return 0;	
}


walk()
{
	var slowTurnSpd, slowTurnSpd2;

	slowTurnSpd = mATurnSpd2 / 4;
	slowTurnSpd2 = mATurnSpd2 / 12;
	
	if (isJumping) {
		move pelvis to y-axis [0.000000] speed [12.500000];
		turn pelvis to x-axis <0.000000> speed <20.000000>;
		turn rthigh to x-axis <0.000000> speed <200.000000>;
		turn rleg to x-axis <0.000000> speed <200.000000>;
		turn rfoot to x-axis <0.000000> speed <200.000000>;
		turn lthigh to x-axis <0.000000> speed <200.000000>;
		turn lleg to x-axis <0.000000> speed <200.000000>;
		turn lfoot to x-axis <0.000000> speed <200.000000>;
		sleep 100;
		return;
	}
	// back contact point : right foot behind, left foot forward
	if( bMoving && !isJumping)
	{
		turn lthigh to x-axis <0.000000>-legAngle1 speed mATurnSpd1;
		turn lleg to x-axis legAngle2 speed mATurnSpd2;
		turn lfoot to x-axis <0> speed mATurnSpd3;

		turn rthigh to x-axis <-7>+legAngle2 speed mATurnSpd2;
		turn rleg to x-axis legAngle3 speed mATurnSpd2;
		turn rfoot to x-axis <0.000000>-legAngle2 speed mATurnSpd1;

		move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		turn pelvis to x-axis <4.000000> speed slowTurnSpd2;
		if(!bAiming)
		{
			// turn torso left, right arm forward
			turn torso to y-axis <10.000000> speed slowTurnSpd2;
			turn luparm to x-axis <25.000000> speed slowTurnSpd;
			turn ruparm to x-axis <0.000000> speed slowTurnSpd;
			//turn rloarm to x-axis <-20.000000> speed slowTurnSpd2;
		}
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 2 : left stomp ground
	if( bMoving && !isJumping)
	{
		turn lfoot to x-axis legAngle3 speed mATurnSpd1*5;
		wait-for-turn lfoot around x-axis;
	}

	// passing pose 2 : right foot raising, left foot on the ground
	if( bMoving && !isJumping)
	{
		turn lthigh to x-axis <0.000000> speed mATurnSpd2;
		turn lleg to x-axis <0.000000> speed mATurnSpd2;
		turn lfoot to x-axis <0.000000> speed mATurnSpd3;

		turn rthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn rleg to x-axis <30.000000> speed mATurnSpd2;
		turn rfoot to x-axis <10.000000> speed mATurnSpd1;

		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn luparm to x-axis <10.000000> speed slowTurnSpd;
			turn ruparm to x-axis <10.000000> speed slowTurnSpd;
			//turn rloarm to x-axis <-15.000000> speed slowTurnSpd2;

		}

		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;		
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;

	}
	
	// forward contact point : right foot forward, left foot behind
	if( bMoving && !isJumping)
	{
		turn lthigh to x-axis <-7>+legAngle2 speed mATurnSpd2;
		turn lleg to x-axis legAngle3 speed mATurnSpd2;
		turn lfoot to x-axis <0>-legAngle2 speed mATurnSpd1;

		turn rthigh to x-axis <0>-legAngle1 speed mATurnSpd1;
		turn rleg to x-axis legAngle2 speed mATurnSpd2;
		turn rfoot to x-axis <0> speed mATurnSpd3;
		
		move pelvis to y-axis mAVertFactor speed mAMoveSpd1;
		turn pelvis to x-axis <4.000000> speed slowTurnSpd2;
		
		if(!bAiming)
		{
			// turn torso right, left arm forward
			turn torso to y-axis <-10.000000> speed slowTurnSpd2;
			turn luparm to x-axis <0.000000> speed slowTurnSpd;
			turn ruparm to x-axis <25.000000> speed slowTurnSpd;
			//turn rloarm to x-axis <-10.000000> speed slowTurnSpd2;
		}

		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}

	// intermediate pose 1 : right stomp ground
	if( bMoving && !isJumping)
	{
		turn rfoot to x-axis legAngle3 speed mATurnSpd1*5;
		wait-for-turn rfoot around x-axis;
	}

	// passing pose 1 : right foot on ground, left foot raising
	if( bMoving && !isJumping)
	{
		turn lthigh to x-axis <-20.000000> speed mATurnSpd2;
		turn lleg to x-axis <30.000000> speed mATurnSpd2;
		turn lfoot to x-axis <10.000000> speed mATurnSpd1;
		
		turn rthigh to x-axis <0.000000> speed mATurnSpd2;
		turn rleg to x-axis <0.000000> speed mATurnSpd2;
		turn rfoot to x-axis <0.000000> speed mATurnSpd3;

		
		if(!bAiming)
		{
			// restore position
			turn torso to y-axis <0.000000> speed slowTurnSpd2;
			turn luparm to x-axis <10.000000> speed slowTurnSpd;
			turn ruparm to x-axis <10.000000> speed slowTurnSpd;
			//turn rloarm to x-axis <-15.000000> speed slowTurnSpd2;
		}
		
		move pelvis to y-axis [0.000000] speed mAMoveSpd1;
		turn pelvis to x-axis <0.000000> speed slowTurnSpd2;
		
		wait-for-turn lthigh around x-axis;
		wait-for-turn rthigh around x-axis;
		wait-for-turn lleg around x-axis;
		wait-for-turn rleg around x-axis;
	}
	
	return;
}


MotionControl()
{
	var moving, aiming, justMoved;
	justMoved = TRUE;
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			justMoved = TRUE;
			if( aiming )
			{
				call-script walk();
			}
			if( !aiming )
			{
				call-script walk();
			}
			
		}
		if( !moving )
		{
			if( justMoved )
			{
				move pelvis to y-axis [0.000000] speed [12.500000];
				turn pelvis to x-axis <0.000000> speed <20.000000>;
				turn rthigh to x-axis <0.000000> speed <200.000000>;
				turn rleg to x-axis <0.000000> speed <200.000000>;
				turn rfoot to x-axis <0.000000> speed <200.000000>;
				turn lthigh to x-axis <0.000000> speed <200.000000>;
				turn lleg to x-axis <0.000000> speed <200.000000>;
				turn lfoot to x-axis <0.000000> speed <200.000000>;
				
				if( !aiming )
				{
					turn torso to y-axis <0.000000> speed <200.000000>;
					turn luparm to x-axis <0.000000> speed <200.000000>;
					turn ruparm to x-axis <0.000000> speed <200.000000>;
				}
				justMoved = FALSE;
			}
			sleep 100;
		}
	}
	return;
}

Create()
{
	hide rflare;
	hide lflare;
	hide lfirept;
	hide rfirept;
	hide ltorpgun;
	hide ltorpedo;
	hide rtorpgun;
	hide rtorpedo;

	bMoving = FALSE;
	bBuilding = FALSE;
	bDgunning = FALSE;
	bUnderwater = FALSE;
	canAim1 = TRUE;
	canAim2 = FALSE;
	gun_1=[0];
	gun_2=[0];
	nano1=[0];
	bAiming = 0;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script initJump();
	start-script MotionSpeedControl();	
	start-script MotionControl();
	start-script Build();
	start-script checkUnderwater();
	return;
}

StartMoving()
{
	bMoving = TRUE;
	return;
}

StopMoving()
{
	bMoving = FALSE;
	return;
}

SweetSpot(piecenum)
{
	piecenum = torso;
	return;
}

QueryNanoPiece(piecenum)
{
	if (nano1 == [0]) {
		piecenum = lnanospray;
		nano1 = [1];
		return;
	}
	if (nano1 == [1]) {
		piecenum = rnanospray;
		nano1 = [0];
		return;
	}	
}

RestorePosition()
{
	bDgunning = FALSE;
	sleep RESTORE_DELAY;
	call-script stopAimHelper1(<290>,<250>);
	turn torso to y-axis <0.000000> speed <290.000000>;
	turn ruparm to x-axis <0.000000> speed <245.000000>;
	turn luparm to x-axis <0.000000> speed <245.000000>;
	wait-for-turn torso around y-axis;
	wait-for-turn ruparm around x-axis;
	wait-for-turn luparm around x-axis;
	bAiming = FALSE;
	return;
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
	return;
}

QueryPrimary(piecenum)
{
	if (gun_1 == [0]) {
		piecenum = lflare;
		return;
	}
	if (gun_1 == [1]) {
		piecenum = rflare;
		return;
	}
}

AimPrimary(heading, pitch)
{
	if( bDgunning || bBuilding || !canAim1)
	{
		return (0);
	}
	call-script updateAimHelper1(heading,pitch,<300>,<250>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;

	turn torso to y-axis heading speed <250.093407>;
	turn luparm to x-axis <0.000000> - pitch speed <245.000000>;
	turn ruparm to x-axis <0.000000> - pitch speed <245.000000>;
	turn lgun to x-axis <0.000000> speed <245.000000>;
	turn rgun to x-axis <0.000000> speed <245.000000>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn lgun around x-axis;
		wait-for-turn rgun around x-axis;
	}
	start-script RestorePosition();
	return (1);
}

FirePrimary()
{
	if (gun_1 == [0]) {
		turn larm to x-axis <20.000000> now;
		turn lgun to x-axis <-30.000000> now;
		emit-sfx 1024 from lflare;	
		sleep 250;
		turn larm to x-axis <0.000000> speed <130.038462>;
		turn lgun to x-axis <0.000000> speed <260.038462>;
		gun_1 = [1];
		return;
	}
	if (gun_1 == [1]) {
		turn rarm to x-axis <20.000000> now;
		turn rgun to x-axis <-30.000000> now;
		emit-sfx 1024 from rflare;	
		sleep 250;
		turn rarm to x-axis <0.000000> speed <130.038462>;
		turn rgun to x-axis <0.000000> speed <260.038462>;
		gun_1 = [0];
		return;
	}	
}


AimFromSecondary(piecenum)
{
	piecenum = torso;
	return;
}

QuerySecondary(piecenum)
{
	if(gun_2 == [0]) {
		piecenum = lfirept;
	}
	if(gun_2 == [1]) {
		piecenum = rfirept;
	}
	return;
}

AimSecondary(heading, pitch)
{
	if( bDgunning || bBuilding || !canAim2)
	{
		return (0);
	}
	call-script updateAimHelper1(heading,pitch,<300>,<250>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;

	turn torso to y-axis heading speed <300.093407>;
	turn luparm to x-axis <0.000000> - pitch speed <245.000000>;
	turn ruparm to x-axis <0.000000> - pitch speed <245.000000>;
	turn lgun to x-axis <0.000000> speed <245.000000>;
	turn rgun to x-axis <0.000000> speed <245.000000>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn luparm around x-axis;
		wait-for-turn ruparm around x-axis;
	}
	start-script RestorePosition();
	return (1);
}

FireSecondary()
{
	if (gun_2 == [0]) {
		move ltorpedo to z-axis [-3.500000] now;
		sleep 100;
		gun_2 = [1];
		sleep 900;
		move ltorpedo to z-axis [0.000000] speed [9.000000];
		return;
	}
	if (gun_2 == [1]) {
		move rtorpedo to z-axis [-3.500000] now;
		sleep 100;
		gun_2 = [0];
		sleep 900;
		move rtorpedo to z-axis [0.000000] speed [9.000000];
		return;
	}
}



StartBuilding(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<300>,<250>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bBuilding = TRUE;
	bAiming = TRUE;
	turn torso to y-axis heading speed <300.093407>;
	turn luparm to x-axis <0.000000> - pitch speed <250.000000>;
	turn ruparm to x-axis <0.000000> - pitch speed <250.000000>;
	turn lgun to x-axis <0.000000> speed <295.000000>;
	turn rgun to x-axis <0.000000> speed <295.000000>;
	move lnano to x-axis [0.850000] speed [6.000000];
	move rnano to x-axis [-0.850000] speed [6.000000];
	move lnano to y-axis [1.400000] speed [10.000000];
	move rnano to y-axis [1.400000] speed [10.000000];
	wait-for-turn torso around y-axis;
	wait-for-turn luparm around x-axis;
	wait-for-turn ruparm around x-axis;
	
	set INBUILDSTANCE to 1;
	return;
}


StopBuilding()
{
	set INBUILDSTANCE to 0;
	bBuilding = FALSE;
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	sleep 1000;
	move lnano to x-axis [0.000000] speed [5.000000];
	move rnano to x-axis [0.000000] speed [5.000000];
	move lnano to y-axis [0.000000] speed [10.000000];
	move rnano to y-axis [0.000000] speed [10.000000];
	start-script RestorePosition();
	return;
}

Killed(Func_Var_1, Func_Var_2)
{
	play-sound( "VICTORY2", 60 );
	sleep 1;
	play-sound( "VICTORY2", 60 );
	return;
}
