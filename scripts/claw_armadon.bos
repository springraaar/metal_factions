#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 	80

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>

piece  gp, base, flash, flash2, flash3, turret, holder, sleeve, barrel, barrel2, barrel3, base2,l1,l2,l3,l4,larmor1,rarmor1,farmor1,barmor1, t1, t2, t1cover, t2cover;

static-var  gun_1;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2

#include "tankthreads.h"

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

Create()
{
	call-script stopAimHelper1(<1000>,<1000>);
	start-script SmokeUnit();
	start-script AnimateTracks();
	gun_1 = [0];
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<90>,<90>);
	turn turret to y-axis <0.000000> speed <90.016484>;
	turn sleeve to x-axis <0.000000> speed <90.005495>;
}

AimPrimary(heading, pitch)
{
	call-script updateAimHelper1(heading,pitch,<150>,<150>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <150.038462>;
	turn sleeve to x-axis <0.000000> - pitch speed <150.000000>;
	if (!aimReady1)
	{
		wait-for-turn turret around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	if(gun_1 == [0])
	{
		move barrel to z-axis [-3.500000] now;
		emit-sfx 1024 from flash;
		sleep 100;
		gun_1 = [1];
		move barrel to z-axis [0.000000] speed [9.000000];
		return (0);
	}
	if(gun_1 == [1])
	{
		move barrel2 to z-axis [-3.500000] now;
		emit-sfx 1024 from flash2;
		sleep 100;
		gun_1 = [2];
		move barrel2 to z-axis [0.000000] speed [9.000000];
		return (0);
	}
	if(gun_1 == [2])
	{
		move barrel3 to z-axis [-3.500000] now;
		emit-sfx 1024 from flash3;
		sleep 100;
		gun_1 = [0];
		move barrel3 to z-axis [0.000000] speed [9.000000];
		return (0);
	}		
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if(gun_1 == [0])
	{
		piecenum = flash;
	}
	if(gun_1 == [1])
	{
		piecenum = flash2;
	}
	if(gun_1 == [2])
	{
		piecenum = flash3;
	}	
}

Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode barrel type SHATTER;
		explode sleeve type SHATTER;
		explode flash type SHATTER;
		explode turret type SHATTER;
		return (1);
	}
	explode barrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode sleeve type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode base type SHATTER;
	explode flash type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3;
	explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP4;
	explode holder type SHATTER;
	explode larmor1 type SHATTER;
	explode rarmor1 type SHATTER;
	return (2);
}
