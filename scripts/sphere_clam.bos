#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, base2,turret,slpt, sleeve, barrel, flash,rdoor,ldoor;


// Signal definitions
#define SIG_AIM				2

lua_stopFiring() { return 0; }

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

open()
{
	turn rdoor to z-axis <-70.000000> speed <100.000000>;
	turn ldoor to z-axis <70.000000> speed <100.000000>;
	wait-for-turn rdoor around z-axis;
	wait-for-turn ldoor around z-axis;
	SET ARMORED to FALSE;	
}

close()
{
	turn rdoor to z-axis <0.000000> speed <100.000000>;
	turn ldoor to z-axis <0.000000> speed <100.000000>;
	wait-for-turn rdoor around z-axis;
	wait-for-turn ldoor around z-axis;
	SET ARMORED to TRUE;	

}

RestoreAfterDelay()
{
	if((get STANDINGFIREORDERS) > 0)
	{
		sleep DEFAULT_WEAPON_RESTORE_DELAY;
	} 
	else 
	{
		sleep 600;
	}
	turn turret to y-axis <0.000000> speed <90.016484>;
	turn slpt to x-axis <0.000000> speed <50.005495>;
	call-script close();
}


// if fire state changes to hold fire, force closure
checkClose(fireState,previousFireState)
{
	while (TRUE)
	{
		fireState = (get STANDINGFIREORDERS);
		
		if (fireState == 0 && previousFireState > 0) 
		{
			signal SIG_AIM;
			call-script lua_stopFiring();
			call-script RestoreAfterDelay();
		}
	
		previousFireState = fireState;
		sleep 100;
	}
}

Create()
{
	hide flash;
	start-script SmokeUnit();
	start-script checkClose();
}


AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	call-script open();
	turn turret to y-axis heading speed <150.038462>;
	turn slpt to x-axis <0.000000> - pitch speed <150.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn slpt around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
	return (0);
}

AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	piecenum = flash;
}

Killed(severity, corpsetype)
{
	hide flash;
	if( severity <= 50 )
	{
		explode barrel type SHATTER;
		explode sleeve type SHATTER;
		explode turret type SHATTER;
		return (1);
	}
	explode barrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode sleeve type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
	explode base type BITMAPONLY | BITMAP2;
	explode turret type SHATTER | EXPLODE_ON_HIT | BITMAP4;
	return (2);
}
