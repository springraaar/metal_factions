#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

#define SPEEDUP_FACTOR 135

#define HEADING_TOLERANCE	<25>
#define PITCH_TOLERANCE		<15>


piece  gp, hips, torso, head, h2, luparm,l1, larm, lflare,larm3, ruparm, r1, rarm, rflare, rarm3, t2, rc1, rc2, lc1,lc2, lemit,remit, leg1,bleg,leg2,bleg2,leg3,bleg3,leg4,bleg4, ljoint, rjoint, t3;

static-var  bMoving, bAiming, isOpen, bUnderwater, gun_1,gun_2, mASpdFactor;

#include "aimhelper1.h"

// Signal definitions
#define SIG_AIM				2
#define SIG_MOVE			4


MotionSpeedControl(minSpeed, maxSpeed,currentSpeed)
{
	maxSpeed = get MAX_SPEED;
	currentSpeed = 0;
	mASpdFactor = SPEEDUP_FACTOR;
	minSpeed = 3 * SPEEDUP_FACTOR / 4;
	
	while(TRUE)
	{
		currentSpeed = get CURRENT_SPEED;
		mASpdFactor = SPEEDUP_FACTOR * currentSpeed / maxSpeed;

		if ( mASpdFactor < minSpeed )
		{
			mASpdFactor = minSpeed;
		}

		sleep 60;
	}
}

checkUnderwater()
{
	var currentHeight;
	while(TRUE)
	{   
 		currentHeight = GET GROUND_WATER_HEIGHT(GET UNIT_XZ);
		
		if ( currentHeight < [-30])
 		{
 			bUnderwater = TRUE;
		
 		}
 		if (currentHeight >= [-30])
 		{
 			bUnderwater = FALSE;
 		}
 		
 		sleep 500;
	}
	return 0;	
}
Open()
{
	if(!isOpen)
	{
		move lc1 to x-axis [-2.500000] speed [15.000000];
		move lc2 to x-axis [2.500000] speed [15.000000];
		move rc1 to x-axis [-2.500000] speed [15.000000];
		move rc2 to x-axis [2.500000] speed [15.000000];
		wait-for-move lc1 along x-axis;
		
		isOpen = TRUE;
	}	
	return (0);
}

Close()
{
	if(isOpen)
	{
		isOpen = FALSE;
		move lc1 to x-axis [0.000000] speed [5.000000];
		move lc2 to x-axis [0.000000] speed [5.000000];
		move rc1 to x-axis [0.000000] speed [5.000000];
		move rc2 to x-axis [0.000000] speed [5.000000];
		wait-for-move lc1 along x-axis;
	}
	return (0);
}


walk()
{
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <5.000000> * mASpdFactor / 100;
			
		/* Y */
		turn leg4 to y-axis <-80.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-35.000000> speed <120.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <5.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <25.000000> speed <260.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <-20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <5.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [-0.2500000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.8500000] speed [14.000000] * mASpdFactor / 100;
		
		
		turn leg4 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg4 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <1.000000> speed <5.000000> * mASpdFactor / 100;
		
		/* Y */
		turn leg4 to y-axis <-35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-90.000000> speed <180.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <15.000000> speed <140.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <-5.000000> speed <200.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg4 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <0.000000> speed <5.000000> * mASpdFactor / 100;
		
		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [15.000000] * mASpdFactor / 100;
		
		turn leg1 to z-axis <-20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg1 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		/* X */
		turn hips to x-axis <-1.000000> speed <20.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-5.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <-25.000000> speed <260.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <80.000000> speed <260.000000> * mASpdFactor / 100;
		
		turn leg1 to y-axis <35.000000> speed <120.000000> * mASpdFactor / 100;

		/* Z */
		turn bleg2 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg3 to z-axis <20.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <1.000000> speed <5.000000> * mASpdFactor / 100;


		/* move */
		move hips to x-axis [0.250000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [-0.850000] speed [14.000000] * mASpdFactor / 100;
		
		turn leg3 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg3 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;

	}
	if( bMoving )
	{
		turn hips to x-axis <1.000000> speed <20.000000> * mASpdFactor / 100;

		/* Y */
		turn leg4 to y-axis <-15.000000> speed <140.000000> * mASpdFactor / 100;

		turn leg2 to y-axis <5.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg3 to y-axis <35.000000> speed <200.000000> * mASpdFactor / 100;

		turn leg1 to y-axis <90.000000> speed <180.000000> * mASpdFactor / 100;			

		/* Z */
		turn bleg3 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn bleg2 to z-axis <0.000000> speed <260.000000> * mASpdFactor / 100;
		turn hips to z-axis <0.000000> speed <5.000000> * mASpdFactor / 100;

		/* move */
		move hips to x-axis [0.000000] speed [2.000000] * mASpdFactor / 100;
		move hips to z-axis [0.000000] speed [15.000000] * mASpdFactor / 100;

		turn leg2 to z-axis <20.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
		turn leg2 to z-axis <0.000000> speed <200.000000> * mASpdFactor / 100;
		sleep 100 * 100 / mASpdFactor;
	}
}




SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from torso;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MotionControl(moving, aiming, justmoved)
{
	while( TRUE )
	{
		moving = bMoving;
		aiming = bAiming;
		if( moving )
		{
			justmoved = TRUE;
			call-script walk();
		}
		if( !moving )
		{
			if( justmoved )
			{
				turn leg1 to x-axis <0.000000> speed <260.000000>;
				turn leg2 to x-axis <0.000000> speed <260.000000>;
				turn leg3 to x-axis <0.000000> speed <260.000000>;
				turn leg4 to x-axis <0.000000> speed <260.000000>;
				turn leg1 to y-axis <45.000000> speed <260.000000>;
				turn leg2 to y-axis <-45.000000> speed <260.000000>;
				turn leg3 to y-axis <45.000000> speed <260.000000>;
				turn leg4 to y-axis <-45.000000> speed <260.000000>;
			
				turn leg1 to z-axis <0.000000> speed <260.000000>;
				turn leg2 to z-axis <0.000000> speed <260.000000>;
				turn leg3 to z-axis <0.000000> speed <260.000000>;
				turn leg4 to z-axis <0.000000> speed <260.000000>;
				turn hips to z-axis <0.000000> speed <260.000000>;
				turn hips to x-axis <0.000000> speed <260.000000>;
	
				move hips to x-axis [0.000000] speed [3.000000];
				move hips to z-axis [0.000000] speed [3.000000];

				turn bleg2 to z-axis <0.000000> speed <160.000000>;
				turn bleg4 to z-axis <0.000000> speed <160.000000>;
				turn bleg3 to z-axis <0.000000> speed <160.000000>;
				turn bleg to z-axis <0.000000> speed <160.000000>;

				justmoved = FALSE;
			}
			sleep 100;
		}
	}
}

aimEffect()
{
	while( TRUE )
	{
		if (bAiming)
		{
			emit-sfx 1024+2 from lflare;
			emit-sfx 1024+2 from rflare;
		}
		sleep 100;
	}

}

Create()
{
	bMoving = FALSE;
	bAiming = FALSE;
	isOpen = FALSE;
	bUnderwater = FALSE;
	gun_1 = 0;
	gun_2 = 0;
	call-script stopAimHelper1(<1000>,<1000>);
	start-script MotionSpeedControl();
	start-script MotionControl();
	start-script SmokeUnit();
	start-script aimEffect();
	start-script checkUnderwater();
}


StartMoving()
{
	bMoving = TRUE;
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	while( bMoving )
	{
		if( bUnderwater == TRUE )
		{
			emit-sfx 256 | 3 from leg1;
			emit-sfx 256 | 3 from leg2;
			emit-sfx 256 | 3 from leg3;
			emit-sfx 256 | 3 from leg4;			
		}
		sleep 250;
	}
}

StopMoving()
{
	bMoving = FALSE;
	signal SIG_MOVE;
}

RestoreAfterDelay()
{
	sleep DEFAULT_WEAPON_RESTORE_DELAY;
	call-script stopAimHelper1(<140>,<140>);
	turn torso to y-axis <0.000000> speed <140.016484>;
	turn ruparm to x-axis <0.000000> speed <145.005495>;
	turn luparm to x-axis <0.000000> speed <145.005495>;
	call-script Close();
	bAiming = FALSE;
}



AimFromSecondary(piecenum)
{
	piecenum = torso;
}

QuerySecondary(piecenum)
{
	if (gun_2 == 0)
	{
		piecenum = lemit;
		return(0);
	}
	if (gun_2 == 1)
	{
		piecenum = remit;
		return(0);
	}
}

FireSecondary()
{
	if ( gun_2 == 0)
	{
		emit-sfx 1024+1 from lemit;
		sleep 100;
		gun_2 = 1;
		return (1);
	}
	if (gun_2 == 1)
	{
		emit-sfx 1024+1 from remit;
		sleep 100;
		gun_2 = 0;
		return (1);
	}
}

AimSecondary(heading, pitch)
{
	return (1);
}

AimFromPrimary(piecenum)
{
	piecenum = torso;
}

QueryPrimary(piecenum)
{
	if (gun_1 == 0)
	{
		piecenum = lflare;
		return(0);
	}
	if (gun_1 == 1)
	{
		piecenum = rflare;
		return(0);
	}
}

FirePrimary()
{
	if(gun_1 == 0)
	{
		turn l1 to x-axis <5.000000> now;
		turn larm to x-axis <-5.000000> now;
		emit-sfx 1024 from lflare;	
		sleep 150;
		turn l1 to x-axis <0.000000> speed <130.038462>;
		turn larm to x-axis <0.000000> speed <130.038462>;
		gun_1 = 1;
		return (0);
	}
	if(gun_1 == 1)
	{
		turn r1 to x-axis <5.000000> now;
		turn rarm to x-axis <-5.000000> now;
		emit-sfx 1024 from rflare;	
		sleep 150;
		turn r1 to x-axis <0.000000> speed <130.038462>;
		turn rarm to x-axis <0.000000> speed <130.038462>;
		gun_1 = 0;
		return (0);
	}
}

AimPrimary(heading, pitch)
{
	if(!isOpen)
	{
		call-script Open();
	}
	call-script updateAimHelper1(heading,pitch,<200>,<200>,HEADING_TOLERANCE,PITCH_TOLERANCE);
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	bAiming = TRUE;

	
	turn torso to y-axis heading speed <200.027473>;
	turn ruparm to x-axis <0.000000> - pitch speed <200.038462>;
	turn luparm to x-axis <0.000000> - pitch speed <200.038462>;
	if (!aimReady1)
	{
		wait-for-turn torso around y-axis;
		wait-for-turn ruparm around x-axis;
		wait-for-turn luparm around x-axis;
	}
	start-script RestoreAfterDelay();
	return (1);
}



QueryTertiary(piecenum)
{
	piecenum = torso;
	return (0);
}

AimFromTertiary(piecenum)
{
	piecenum = torso;
	return (0);
}

AimTertiary(heading, pitch)
{
	return (1);
}


Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode larm type SHATTER;
		explode rarm type SHATTER;
		return (1);
	}

	explode torso type SHATTER;
	return (2);
}
