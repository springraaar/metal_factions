#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, spinner,b2,s1,s2,s3,s4, watercover, watercover2, waterspinner, emit1, emit2, emit3;

static-var  statechg_DesiredState, statechg_StateChanging, rotSpeed, bUnderwater;

// signal definitions
#define SIG_ACTIVATE 	2

#define STATE_OFF	0
#define STATE_ON	1

checkUnderwater()
{
	var currentHeight, oldState;
	while(TRUE)
	{   
 		currentHeight = GET GROUND_WATER_HEIGHT(GET UNIT_XZ);
 		oldState = bUnderwater;
		
		// check current state
		if ( currentHeight < [-30])
 		{
 			bUnderwater = TRUE;
		
 		}
 		if (currentHeight >= [-30])
 		{
 			bUnderwater = FALSE;
 		}
 		
 		// handle state changes
 		if (oldState != bUnderwater)
 		{
			if(bUnderwater)
			{
				// hide land stuff
				hide spinner;
				hide s1;
				hide s2;
				hide s3;
				hide s4;
								
				// show water stuff
 				show waterspinner;
 				show watercover;
 				show watercover2;

				sleep 200;
			}
			if(!bUnderwater)
			{
				// hide water stuff
 				hide watercover;
 				hide watercover2;
 				hide waterspinner;

				//show land stuff 			
				show spinner;
 				show s1;
 				show s2;
 				show s3;
 				show s4;
 				
 				sleep 200;	
			}
 		}
 		sleep 500;
	}
	return 0;	
}


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
	// Build by COBBLER Ver4.0 Copyright @1998 DIGITAL CONCEPT SOFTWARE (MANU@inext.fr) / http://www.annihilationcenter.com/DCS/  
}
Go()
{
	if (statechg_DesiredState == STATE_ON)
	{
		if (bUnderwater)
		{
			spin waterspinner around y-axis speed rotSpeed accelerate <30.032967>;
			return (0);
		}
		spin spinner around y-axis speed rotSpeed accelerate <30.032967>;
	}
	return (0);
}

Stop()
{
	if (statechg_DesiredState == STATE_OFF)
	{
		if (bUnderwater)
		{
			stop-spin waterspinner around y-axis decelerate <20.000000>;
			wait-for-turn waterspinner around y-axis;
			return (0);
		}
		stop-spin spinner around y-axis decelerate <20.000000>;
		wait-for-turn spinner around y-axis;
	}
	return (0);
}


InitState()
{
	statechg_DesiredState = STATE_OFF;
	statechg_StateChanging = FALSE;
	return (0);
}

RequestState(requestedstate, currentstate)
{
	if( statechg_StateChanging )
	{
		statechg_DesiredState = requestedstate;
		return (0);
	}
	statechg_StateChanging = TRUE;
	currentstate = statechg_DesiredState;
	statechg_DesiredState = requestedstate;
	while( statechg_DesiredState != currentstate )
	{
		if( statechg_DesiredState == STATE_ON )
		{
			call-script Go();
			currentstate = STATE_ON;
		}
		if( statechg_DesiredState == STATE_OFF )
		{
			call-script Stop();
			currentstate = STATE_OFF;
		}
	}
	statechg_StateChanging = FALSE;
	return (0);
}

Create()
{
	bUnderwater = FALSE;
	hide waterspinner;
	hide watercover;
	hide watercover2;
	start-script checkUnderwater();
	rotSpeed=[0];
	call-script InitState();
	start-script SmokeUnit();
	return (0);
}

Activate()
{
	start-script RequestState(STATE_ON);
	signal SIG_ACTIVATE;
	set-signal-mask SIG_ACTIVATE;
	while( TRUE )
	{
		if(bUnderwater)
		{
			emit-sfx 256 | 3 from emit1;
			emit-sfx 256 | 3 from emit2;
			emit-sfx 256 | 3 from emit3;
		}
		sleep 250;		
	}
	return (0);
}

Deactivate()
{
	start-script RequestState(STATE_OFF);
	signal SIG_ACTIVATE;
	return (0);
}

SetSpeed(modifier)
{
	rotSpeed = modifier * 13;
	return (0);
}


Killed(severity, corpsetype)
{
	if( severity <= 50 )
	{
		explode spinner type SHATTER;
		return (1);
	}
	
	explode base type SHATTER;
	explode spinner type SHATTER;
	explode s1 type SHATTER;
	explode s2 type SHATTER;
	explode s3 type SHATTER;
	explode s4 type SHATTER;
	return (2);
}
